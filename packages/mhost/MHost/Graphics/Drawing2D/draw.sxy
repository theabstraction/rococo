(using Sys.Maths)

(using MHost)
(using MHost.Graphics)
(using MHost.Graphics.Utils)
(using MPlat)

(function DrawUpTriangle (IGui g)(Rectf bounds)(Int32 colour)-> :
	(Float32 xC = (0.5 * (bounds.left + bounds.right)))
	
	(BaseVertexData nFont = (0 0) 0)
	(SpriteVertexData noSprite = 1 0 0 0)
	
	(GuiTriangle tri = 
		((xC bounds.top)				nFont noSprite colour)
		((bounds.left bounds.bottom)	nFont noSprite colour)
		((bounds.right bounds.bottom)	nFont noSprite colour)
	)
	
	(g.DrawTriangle tri)
)

(function DrawDownTriangle (IGui g)(Rectf bounds)(Int32 colour)-> :
	(Float32 xC = (0.5 * (bounds.left + bounds.right)))
	
	(BaseVertexData nFont = (0 0) 0)
	(SpriteVertexData noSprite = 1 0 0 0)
	
	(GuiTriangle tri = 
		((xC bounds.bottom)			nFont noSprite colour)
		((bounds.left bounds.top)	nFont noSprite colour)
		((bounds.right bounds.top)	nFont noSprite colour)
	)
	
	(g.DrawTriangle tri)
)

(function DrawStandardControlBorder (IGui gui)(Rectf rect)(Bool isEnabled)(Bool isGlowing)-> :
	(Vec2 cursorPos)
	(gui.GetCursorPos cursorPos)
	
	(if (isGlowing)
		(Int64 tickHz = (Sys.Time.TickHz))
		(Int64 tickCount = (Sys.Time.TickCount))
		(Int64 ticksPerDelta = (tickHz / 145))
		(Int64 t = (tickCount / ticksPerDelta))
		(Int32 i = ((Sys.Maths.I64.ToInt32 (Sys.Maths.I64.Mod t 128)) + 127))
		(Int32 colour = (Sys.Maths.I32.FromARGB 0xFF i i i))
		
		(gui.DrawBorder rect 2 colour colour colour colour)
	else
		(if (not isEnabled)
			(gui.DrawBorder rect 1 0xFF101010 0xFF101010 0xFF101010 0xFF101010)
		else
			(if (IsPointInRect cursorPos rect)
				(gui.DrawBorder rect 1 0xFFC0C0C0 0xFFA0A0A0 0xFF808080 0xFF606060)
			else
				(gui.DrawBorder rect 1 0xFFA0A0A0 0xFF808080 0xFF606060 0xFF404040)
			)
		)
	)
)

(function GetTextColourAtRectUnderCursor (Rectf rect)(IGui g)(Bool isEnabled)-> (Int32 colour):
	(Vec2 cursorPos)
	(g.GetCursorPos cursorPos)
	
	(if (not isEnabled)
		(colour = 0xFF101010)
	else 
		(if (IsPointInRect cursorPos rect)
			(colour = 0xFFFFFFFF)
		else
			(colour = 0xFFC0C0C0)
		)
	)
)

(alias GetTextColourAtRectUnderCursor $.GetTextColourAtRectUnderCursor)
(alias DrawStandardControlBorder $.DrawStandardControlBorder)
(alias DrawUpTriangle $.DrawUpTriangle)
(alias DrawDownTriangle $.DrawDownTriangle)