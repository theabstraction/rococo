(using MHost)
(using MHost.Graphics)
(using MHost.Graphics.Utils)
(using MHost.Graphics.Widgets)
(using MHost.Events)

(using Sys.Maths)
(using Sys.Maths.F32)
(using Sys.Type)
(using Sys.Type.Strings)

(struct QuadColour
	(Int32 fontColour)
	(Int32 backColour)
	(Int32 tl tr bl br)
)

(alias QuadColour $.QuadColour)

(function UseDefaultColourScheme (QuadColour low)(QuadColour high)-> :
	(low  = 0xFFA0A0A0 0xFF000000 0xFF606060 0xFF808080 0xFFA0A0A0 0xFFC0C0C0)
	(high = 0xFFFFFFFF 0xFF000000 0xFF808080 0xFFA0A0A0 0xFFC0C0C0 0xFFFFFFFF)
)

(alias UseDefaultColourScheme $.UseDefaultColourScheme)

(struct MenuItem
	(IString text)
	(Rectf rect)
	(IWidgetContext wc)
)

(interface $.IVerticalMenu (extends $.IMenu)
	(OnMouseUp (MouseClickArgs args)->)
)

(class VerticalMenu
	(implements $.IVerticalMenu)
	(implements IEventHandler)
	(array MenuItem items)
	(Int32 alignment)
	(Int32 fontIndex)
	(Float32 borderThickness)
	(QuadColour coloursLow)
	(QuadColour coloursHigh)
	(QuadColour bkLow)
	(QuadColour bkHigh)
	(Rectf outerRect)
	(Vec2 cursorPos)
	(IString fontFamily)
)

(factory $.NewVerticalMenu $.IMenu (Int32 maxEntries) :
	(construct VerticalMenu maxEntries)
)

(method VerticalMenu.Construct (Int32 maxEntries):
	(array MenuItem items maxEntries)
	(this.items = items)
	(this.alignment = 0)
	(this.fontIndex = 0)
	(this.fontFamily = "Courier New")
	(UseDefaultColourScheme this.coloursLow this.coloursHigh)
	(UseDefaultColourScheme this.bkLow this.bkHigh)
)

(method VerticalMenu.AddTextItem (IString text)(IWidgetContext wc)->(Int32 id):
	(MenuItem item = text (0 0 0 0) wc)
	(this.items.Push item)
)

(method VerticalMenu.SetColourScheme (QuadColour low)(QuadColour high)(QuadColour bkLow)(QuadColour bkHigh)-> :
	(this.coloursLow = low)
	(this.coloursHigh = high)
	(this.bkLow = bkLow)
	(this.bkHigh = bkHigh)
	(this.alignment = 0)
) 

(method VerticalMenu.SetStyle (Int32 textAlignment)(Float32 borderThickness)(IString fontFamily)-> :
	(this.alignment = textAlignment)
	(this.borderThickness = borderThickness)
	(this.fontFamily = fontFamily)
)

(method VerticalMenu.LayoutFixedRect (Rectf rect)(IGui g)-> :

	(Vec2 span)
	(GetSpanRectf rect span)
	
	(Int32 nItems = (this.items.Length))

	(if (nItems == 0) (return))

	(Float32 nItemsF32 = (Sys.Maths.I32.ToFloat32 nItems))
	
	(Float32 pxHeightPerRow = (Floor (span.y / nItemsF32)))

	(this.fontIndex = 0)
	
	(Float32 buttonYHeight = pxHeightPerRow)
	(Float32 buttonYBorder = 2)
	
	(Float32 y = (rect.top + buttonYBorder))
	
	(foreach item # this.items
		(item.rect.left = (rect.left + buttonYBorder))
		(item.rect.right = (rect.right - buttonYBorder))
		(item.rect.top = y)
		(item.rect.bottom = (y + buttonYHeight))
		(y += buttonYHeight)
		(y += buttonYBorder)
	)
	
	(this.outerRect = rect.left rect.top rect.right y)
)

(method VerticalMenu.OnMouseUp (MouseClickArgs args)-> :
	(if ((args.button == 0) and (IsPointInRect args.pos this.outerRect))
		(Int32 i = 0)
		(foreach item # this.items
			(if (IsPointInRect args.pos item.rect)
				(MenuSelectArgs menuArgs = i item.text this)
				(args.eventHandler.OnMenuSelect menuArgs)
				(return)
			)
			(i += 1)
		)
	)
)

(method VerticalMenu.GetRect (Rectf rect) -> :
	(rect = this.outerRect)
)

(method VerticalMenu.GetEventHandler -> (IEventHandler handler):
	(handler = this)
)
	
(method VerticalMenu.Render (IGui g)-> :
	(g.GetCursorPos this.cursorPos)
	
	(QuadColour colours)
	
	(if (IsPointInRect this.cursorPos this.outerRect)
		(colours = this.bkHigh)
	else	
		(colours = this.bkLow)
	)
	
	(g.FillRect this.outerRect colours.backColour)
//	(g.DrawBorder this.outerRect this.borderThickness colours.tl colours.tr colours.bl colours.br)
	
	(foreach item # this.items
		(if (IsPointInRect this.cursorPos item.rect)
			(colours = this.coloursHigh)
		else	
			(colours = this.coloursLow)
		)
		
		(g.DrawBorder item.rect 1 colours.tl colours.tr colours.bl colours.br)
		
		(Float32 itemHeight = (item.rect.bottom - item.rect.top))
		(Float32 halfFontHeight = (itemHeight / 2.2))
		
		(Rectf textRect)
		(textRect.left = item.rect.left)
		(textRect.right = item.rect.right)
		
		(Float32 vCentre = (0.5 * (item.rect.top + item.rect.bottom)))
		
		(textRect.top = (vCentre - halfFontHeight))
		(textRect.bottom = (vCentre + halfFontHeight))
		
		(g.DrawText textRect this.alignment item.text this.fontIndex colours.fontColour)
	)
)