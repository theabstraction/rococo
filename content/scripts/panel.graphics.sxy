(' #file.type rococo.mplat.panel)

(' #include 
	"!scripts/mplat_gui_sxh.sxy"
	"!scripts/mplat_types.sxy"
	"!scripts/types.sxy"
	"!scripts/audio_types.sxy"
)

(namespace EntryPoint)
	(alias Main EntryPoint.Main)
	
(using Rococo)
(using Sys.Maths)
(using Sys.Type)

(function SetTransparentBackground (IPane pane)-> :
	(pane.SetColourBk1   0x00000000 0x00000000)
	(pane.SetColourBk2   0x00000000 0x00000000)
	(pane.SetColourEdge1 0x00000000 0x00000000)
	(pane.SetColourEdge2 0x00000000 0x00000000)
)

(function SetOpaqueBackground (IPane pane)-> :
	(pane.SetColourBk1   0xFF000000 0xFF000000)
	(pane.SetColourBk2   0xFF000000 0xFF000000)
	(pane.SetColourEdge1 0x00000000 0x00000000)
	(pane.SetColourEdge2 0x00000000 0x00000000)
)

// Norfolk & Durham, Simon Bailey, Mike Barton, John Glen MP
(function AddLabel (IString text)(IPaneContainer builder)(IString cmd)(Bool defer)-> :
	(Recti rect = 0 0 20 70)
	(ILabelPane label = (builder.AddLabel 0 text rect))
	(label.SetColourFont 0xFFD0D0D0 0xFFFFFFFF)
	(label.SetColourBk1  0x00000000 0xFF200000)
	(label.SetColourBk2  0x00000000 0xFF800000)
	(label.SetColourEdge1 0xFF404040 0xFFBFBFBF)
	(label.SetColourEdge2 0xFF606060 0xFFFFFFFF)
	(label.SetCommand 1 defer cmd)
	(label.SetAlignment 0 0 0 0)
)

(function AddTitleLabel (IString text)(IPaneContainer builder)-> :
	(Recti rect = 0 0 20 90)
	(ILabelPane label = (builder.AddLabel 0 text rect))
	(label.SetColourFont 0xFFFFFFFF 0xFFFFFFFF)
	(label.SetColourBk1  0x00000000 0x00000000)
	(label.SetColourBk2  0x00000000 0x00000000)
	(label.SetColourEdge1 0x00000000 0x00000000)
	(label.SetColourEdge2 0x00000000 0x00000000)
	(label.SetAlignment 0 0 0 0)
)

(function AddSlider (IString text)(IPaneContainer builder)(Float32 minValue)(Float32 maxValue)-> :
	(Recti rect = 0 0 20 90)
	(ISlider slider = (builder.AddSlider 1 text rect minValue maxValue))
	(slider.SetColourFont 0xFFD0D0D0 0xFFFFFFFF)
	(slider.SetColourBk1  0x8F006000 0xFF008000)
	(slider.SetColourBk2  0x8F008800 0xFF00A800)
	(slider.SetColourEdge1 0xDFFFFFFF 0xFFFFFFFF)
	(slider.SetColourEdge2 0xDFFFFFFF 0xFFFFFFFF)
)

(function Main (Int32 id)->(Int32 exitCode):
	(IPaneBuilder builder (PaneBuilder))
	(IPaneContainer pane = builder.Root)
	(SetOpaqueBackground pane)
	
	(Recti screen)
	(pane.GetRect screen)
	
	(Recti containerRect)
	(InflateRect -16 -16 screen containerRect)
	(IPaneContainer container = (pane.AddContainer containerRect))
	(container.SetColourBk1   0xE0101010 0xE0101010)
	(container.SetColourBk2   0xE0202020 0xE0202020)
	
	(AddTitleLabel "Graphics"   container)
	(AddSlider "View Distance"  container 0 1)
	(AddSlider "Object Density" container 0 1)
	(AddSlider "Object Detail" container 0 1)
	
	(container.AlignLeftEdges 40 false)
	(container.AlignRightEdges 40 false)
	(container.LayoutVertically 4 -1)
	
	(Recti menuRect = 10 10 180 140)
	(IPaneContainer menu = (container.AddContainer menuRect))
	(SetTransparentBackground menu)

	(AddLabel "Back" menu "gui.pop" false)
	
	(menu.AlignLeftEdges 4 false)
	(menu.AlignRightEdges 4 false)
	(menu.LayoutVertically 4 -1)
)