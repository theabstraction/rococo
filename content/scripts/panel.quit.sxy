(' #file.type rococo.mplat.panel)

(' #include 
	"!scripts/mplat.sxh.sxy"
	"!scripts/types.sxy"
)

(namespace EntryPoint)
	(alias Main EntryPoint.Main)
	
(using Rococo)
(using Sys.Maths)
(using Sys.Type)

(function SetOpaqueBackground (IPane pane)-> :
	(pane.SetColourBk1   0xFF000000 0xFF000000)
	(pane.SetColourBk2   0xFF000000 0xFF000000)
	(pane.SetColourEdge1 0xFF000000 0xFF000000)
	(pane.SetColourEdge2 0xFF000000 0xFF000000)
)

// Norfolk & Durham, Simon Bailey, Mike Barton, John Glen MP

(function AddTitleLabel (IString text)(IPaneContainer builder)-> :
	(Recti rect = (0 0)(20 90))
	(ILabelPane label = (builder.AddLabel 0 text rect))
	(IPane labelBase = label.Base)
	(labelBase.SetColourFont 0xFFFFFFFF 0xFFFFFFFF)
	(labelBase.SetColourBk1  0x00000000 0x00000000)
	(labelBase.SetColourBk2  0x00000000 0x00000000)
	(labelBase.SetColourEdge1 0x00000000 0x00000000)
	(labelBase.SetColourEdge2 0x00000000 0x00000000)
	(label.SetAlignment 0 0 0 0)
)

(function AddLabel (IString text)(IPaneContainer builder)(IString cmd)(Bool defer)-> :
	(Recti rect = (0 0)(20 90))
	(ILabelPane label = (builder.AddLabel 0 text rect))
	(IPane labelBase = label.Base)
	(labelBase.SetColourFont 0xFFD0D0D0 0xFFFFFFFF)
	(labelBase.SetColourBk1  0x00000000 0xFF200000)
	(labelBase.SetColourBk2  0x00000000 0xFF800000)
	(labelBase.SetColourEdge1 0x00000000 0xFFFFFFFF)
	(labelBase.SetColourEdge2 0x00000000 0xFFFFFFFF)
	(if (cmd.Length > 0)
		(labelBase.SetCommand 1 defer cmd)
	)
	(label.SetAlignment 0 0 0 0)
)

(function AddVec2iVec2i (Vec2i a)(Vec2i b)(Vec2i sum)-> :
	(sum.x = (a.x + b.x))
	(sum.y = (a.y + b.y))
)

(function SubtractVec2iVec2i (Vec2i a)(Vec2i b)(Vec2i dif)-> :
	(dif.x = (a.x - b.x))
	(dif.y = (a.y - b.y))
)

(function GetCentre (Recti rect)(Vec2i centre)-> :
	(Vec2i sum = rect.topLeft + rect.bottomRight)
	(centre.x = (sum.x / 2))
	(centre.y = (sum.y / 2))
)

(function GetSpan (Recti rect)(Vec2i span) -> :
	(span.x = (rect.bottomRight.x - rect.topLeft.x))
	(span.y = (rect.bottomRight.y - rect.topLeft.y))
)

(function GetCentredRect (Int32 dx)(Int32 dy)(Recti container)(Recti centredRect)-> :
	(Vec2i span)
	(GetSpan container span)
	(Int32 dx2 = (dx / 2))
	(Int32 dy2 = (dy / 2))
	(Vec2i centre)
	(centre.x = (span.x / 2))
	(centre.y = (span.y / 2))
	(Vec2i halfSpan = dx2 dy2)
	(Vec2i topLeft = centre - halfSpan)
	(Vec2i bottomRight = centre + halfSpan)
	(centredRect.topLeft = topLeft)
	(centredRect.bottomRight = bottomRight)
)

(function InflateRect (Int32 dx)(Int32 dy)(Recti container)(Recti rect)-> :
	(rect.topLeft.x = (0 - dx))
	(rect.topLeft.y = (0 - dy))
	
	(Vec2i span)
	(GetSpan container span)
	(rect.bottomRight.x = (span.x + dx))
	(rect.bottomRight.y = (span.y + dy))
)
	
(function Main (Int32 id)->(Int32 exitCode):
	(IPaneBuilder builder (PaneBuilder))
	(IPaneContainer root = builder.Root)
	(IPane pane = root.Base)
	(SetOpaqueBackground pane)
	
	(Recti screen)
	(pane.GetRect screen)
	
	(Recti containerRect)
	(InflateRect -16 -16 screen containerRect)
	(IPaneContainer container = (root.AddContainer containerRect))
	(IPane cbase = container.Base)
	(cbase.SetColourBk1   0xE0101010 0xE0101010)
	(cbase.SetColourBk2   0xE0202020 0xE0202020)
	
	(Recti menuRect)
	(GetCentredRect 800 400 containerRect menuRect)
	
	(IPaneContainer menu = (container.AddContainer menuRect))
	(IPane mbase = menu.Base)
	(mbase.SetColourBk1   0xE0101010 0xE0101010)
	(mbase.SetColourBk2   0xE0202020 0xE0202020)
	
	(AddTitleLabel "Quit Game. Are you sure? " menu)
	(AddLabel "Yes" menu "app.quit" true)
	(AddLabel "Back" menu "gui.pop" false)

	(mbase.AlignLeftEdges 4 false)
	(mbase.AlignRightEdges 4 false)
	(mbase.LayoutVertically 4 -1)
)