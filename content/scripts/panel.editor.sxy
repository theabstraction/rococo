(' #file.type rococo.mplat.panel)

(' #include 
	"!scripts/mplat.pane.sxh.sxy"
	"!scripts/types.sxy"
)

(namespace EntryPoint)
	(alias Main EntryPoint.Main)
	
(using Rococo)
(using Sys.Maths)
(using Sys.Type)

(function SetTransparentBackground (IPane pane)-> :
	(pane.SetColourBk1   0x00000000 0x00000000)
	(pane.SetColourBk2   0x00000000 0x00000000)
	(pane.SetColourEdge1 0x00000000 0x00000000)
	(pane.SetColourEdge2 0x00000000 0x00000000)
)

(function SetOpaqueBackground (IPane pane)-> :
	(pane.SetColourBk1   0xFF000000 0xFF000000)
	(pane.SetColourBk2   0xFF000000 0xFF000000)
	(pane.SetColourEdge1 0x00000000 0x00000000)
	(pane.SetColourEdge2 0x00000000 0x00000000)
)

(function AddLabel (IString text)(IPaneContainer builder)(IString cmd)(Bool defer)-> :
	(Recti rect = 0 0 20 16)
	(ILabelPane label = (builder.AddLabel 9 text rect))
	(IPane labelBase = label.Base)
	(labelBase.SetColourFont 0xFFD0D0D0 0xFFFFFFFF)
	(labelBase.SetColourBk1  0x00000000 0xFF200000)
	(labelBase.SetColourBk2  0x00000000 0xFF800000)
	(labelBase.SetColourEdge1 0xFF404040 0xFFBFBFBF)
	(labelBase.SetColourEdge2 0xFF606060 0xFFFFFFFF)
	(if (cmd.Length > 0)
		(labelBase.SetCommand 1 defer cmd)
	)
	(label.SetAlignment -1 0 0 0)
)

(function AddRadio (IString text)(IPaneContainer builder)(IString key)(IString value)-> :
	(Recti rect = 0 0 20 16)
	(AddRadioAt rect text builder key value -1)
)

(function AddRadioAt (Recti rect)(IString text)(IPaneContainer builder)(IString key)(IString value)(Int32 align)-> :
	(IRadioButton radio = (builder.AddRadioButton 9 text key value rect))
	(IPane labelBase = radio.Base)
	(labelBase.SetColourFont 0xFFD0D0D0 0xFFFFFFFF)
	(labelBase.SetColourBk1  0xFF000020 0xFF0000A0)
	(labelBase.SetColourBk2  0xFF000020 0xFF0000A0)
	(labelBase.SetColourEdge1 0xFF404040 0xFFBFBFBF)
	(labelBase.SetColourEdge2 0xFF606060 0xFFFFFFFF)
	(radio.SetAlignment align 0 0 0)
)

(function AddScroller(IString key)(IPaneContainer container)(Recti rect)-> :
	(IScroller scroller = (container.AddScroller key rect true))
	(IPane sbase = scroller.Base)
	(sbase.SetColourFont  0xFF808080 0xFF909090)
	(sbase.SetColourBk1   0xFF606060 0xFF636363)
	(sbase.SetColourBk2   0xFF606060 0xFF666666)
	(sbase.SetColourEdge1 0xDFFFFFFF 0xFFFFFFFF)
	(sbase.SetColourEdge2 0xDFFFFFFF 0xFFFFFFFF)
)

(function Main (Int32 id)->(Int32 exitCode):
	(IPaneBuilder builder (PaneBuilder))
	(IPaneContainer root = builder.Root)
	(IPane pane = root.Base)
	(SetOpaqueBackground pane)
	
	(Recti screen)
	(pane.GetRect screen)
	
	(Recti containerRect)
	(InflateRect -2 -2 screen containerRect)
	(IPaneContainer container = (root.AddContainer containerRect))
	(IPane cbase = container.Base)
	(SetTransparentBackground cbase)
	(cbase.SetPopulator 0 "sector_editor")
	
	(Recti menuRect = 10 10 110 98)
	(IPaneContainer menu = (container.AddContainer menuRect))
	(IPane mbase = menu.Base)
	(mbase.SetColourBk1   0xAF000000 0xAF000000)
	(mbase.SetColourBk2   0x4F000000 0x4F000000)
	(mbase.SetColourEdge1 0 0)
	(mbase.SetColourEdge2 0 0)
	
	(AddLabel "New" menu "editor.new" false)
	(AddLabel "Load" menu "editor.load" false)
	(AddLabel "Save" menu "editor.save" false)
	(AddRadio "Vertices" menu "editor.edit_mode" "v")
	(AddRadio "Sectors" menu "editor.edit_mode" "s")
	
	(mbase.AlignLeftEdges 4 false)
	(mbase.AlignRightEdges 4 false)
	(mbase.LayoutVertically 4 -1)
	
	(Vec2i containerSpan)
	(GetSpan containerRect containerSpan)
	
	(Recti toolsRect)
	(toolsRect.left = (containerSpan.x - 460))
	(toolsRect.top = 4)
	(toolsRect.right = (containerSpan.x))
	(toolsRect.bottom = (containerSpan.y - 32))
	
	(ITabContainer tabs = (container.AddTabContainer 20 8 toolsRect))
	(IPane tbase = tabs.Base)
	(tbase.SetColourBk1   0xFF808080 0xFF808080)
	(tbase.SetColourBk2   0xFF808080 0xFF808080)
	(tbase.SetColourEdge1 0xFF808080 0xFFFFFFFF)
	(tbase.SetColourEdge2 0xFF808080 0xDFFFFFFF)
	
	(tabs.AddTab 60 "Walls"		"editor.tab.walls")
	(tabs.AddTab 60 "Floor" 	"editor.tab.floor")
	(tabs.AddTab 75 "Ceiling"	"editor.tab.ceiling")
	(tabs.AddTab 60 "Doors"		"editor.tab.doors")
	(tabs.AddTab 64 "Lights"	"editor.tab.lights")
)