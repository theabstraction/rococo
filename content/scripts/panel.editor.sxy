(' #file.type rococo.mplat.panel)

(' #include 
	"!scripts/mplat_pane_sxh.sxy"
	"!scripts/mplat_types.sxy"
	"!scripts/types.sxy"
	"!scripts/audio_types.sxy"
)

(namespace EntryPoint)
	(alias Main EntryPoint.Main)
	
(using Rococo)
(using Sys.Maths)
(using Sys.Type)

(function SetTransparentBackground (IPane pane)-> :
	(pane.SetColourBk1   0x00000000 0x00000000)
	(pane.SetColourBk2   0x00000000 0x00000000)
	(pane.SetColourEdge1 0x00000000 0x00000000)
	(pane.SetColourEdge2 0x00000000 0x00000000)
)

(function SetOpaqueBackground (IPane pane)-> :
	(pane.SetColourBk1   0xFF000000 0xFF000000)
	(pane.SetColourBk2   0xFF000000 0xFF000000)
	(pane.SetColourEdge1 0x00000000 0x00000000)
	(pane.SetColourEdge2 0x00000000 0x00000000)
)

(function AddLabel (IString text)(IPaneContainer builder)(IString cmd)(Bool defer)-> :
	(Recti rect = 0 0 20 26)
	(ILabelPane label = (builder.AddLabel 0 text rect))
	(label.SetColourFont 0xFFD0D0D0 0xFFFFFFFF)
	(label.SetColourBk1  0x00000000 0xFF200000)
	(label.SetColourBk2  0x00000000 0xFF800000)
	(label.SetColourEdge1 0xFF404040 0xFFBFBFBF)
	(label.SetColourEdge2 0xFF606060 0xFFFFFFFF)
	(if (cmd.Length > 0)
		(label.SetCommand 1 defer cmd)
	)
	(label.SetAlignment -1 0 0 0)
)

(function AddRadio (IString text)(IPaneContainer builder)(IString key)(IString value)-> :
	(Recti rect = 0 0 20 26)
	(AddRadioAt rect text builder key value -1)
)

(function AddRadioAt (Recti rect)(IString text)(IPaneContainer builder)(IString key)(IString value)(Int32 align)-> :
	(IRadioButton radio = (builder.AddRadioButton 9 text key value rect))
	(radio.SetColourFont 0xFFD0D0D0 0xFFFFFFFF)
	(radio.SetColourBk1  0xFF000020 0xFF0000A0)
	(radio.SetColourBk2  0xFF000020 0xFF0000A0)
	(radio.SetColourEdge1 0xFF404040 0xFFBFBFBF)
	(radio.SetColourEdge2 0xFF606060 0xFFFFFFFF)
	(radio.SetAlignment align 0 0 0)
)

(function AddScroller(IString key)(IPaneContainer container)(Recti rect)-> :
	(IScroller scroller = (container.AddScroller key rect true))
	(scroller.SetColourFont  0xFF808080 0xFF909090)
	(scroller.SetColourBk1   0xFF606060 0xFF636363)
	(scroller.SetColourBk2   0xFF606060 0xFF666666)
	(scroller.SetColourEdge1 0xDFFFFFFF 0xFFFFFFFF)
	(scroller.SetColourEdge2 0xDFFFFFFF 0xFFFFFFFF)
)

(function Main (Int32 id)->(Int32 exitCode):
	(IPaneBuilder builder (PaneBuilder))
	(IPaneContainer root = builder.Root)
	(SetTransparentBackground root)
	
	(Recti screen)
	(root.GetRect screen)
	
	(Recti containerRect)
	(InflateRect -2 -2 screen containerRect)
	(IPaneContainer container = (root.AddContainer containerRect))
	(SetTransparentBackground container)
	(container.SetPopulator 0 "sector_editor")
	
	(Recti menuRect = 10 10 100 200)
	(IPaneContainer menu = (container.AddContainer menuRect))
	(menu.SetColourBk1   0xAF000000 0xAF000000)
	(menu.SetColourBk2   0x4F000000 0x4F000000)
	(menu.SetColourEdge1 0 0)
	(menu.SetColourEdge2 0 0)
	
	(AddLabel "New" menu "editor.new" false)
	(AddLabel "Load" menu "editor.load" false)
	(AddLabel "Save" menu "editor.save" false)
	(AddRadio "Vertices" menu "editor.edit_mode" "v")
	(AddRadio "Sectors" menu "editor.edit_mode" "s")
	(AddRadio "Opaque" menu "editor.transparency" "o")
	(AddRadio "Clear" menu "editor.transparency" "t")
	
	(menu.AlignLeftEdges 4 false)
	(menu.AlignRightEdges 4 false)
	(menu.LayoutVertically 4 -1)
	
	(Vec2i containerSpan)
	(GetSpan containerRect containerSpan)
	
	(Recti toolsRect)
	(toolsRect.left = (containerSpan.x - 460))
	(toolsRect.top = 4)
	(toolsRect.right = (containerSpan.x))
	(toolsRect.bottom = (containerSpan.y - 32))
	
	(ITabContainer tabs = (container.AddTabContainer 28 0 toolsRect))
	(tabs.SetColourBk1   0xFF808080 0xFF808080)
	(tabs.SetColourBk2   0xFF808080 0xFF808080)
	(tabs.SetColourEdge1 0xFF808080 0xFFFFFFFF)
	(tabs.SetColourEdge2 0xFF808080 0xDFFFFFFF)
	(tabs.SetTabPopulator "editor.tabs")
)