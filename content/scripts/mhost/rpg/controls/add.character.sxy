(using Sys)
(using Sys.Maths)
(using Sys.Maths.F32)
(using Sys.Type)
(using Sys.MPlat)
(using Sys.Random)
(using Sys.Reflection)
(using Sys.Type.Strings)
(using MHost)
(using MHost.OS)
(using MHost.Graphics)
(using Rococo)
(using Rococo.Graphics)
(using Rococo.Audio)
(using RPG)

(interface MHost.Graphics.ILabelButton
	(extends MHost.Graphics.IControl)
	(SetRect (IString fontName) (Rectf rect)(IGui g) -> )
	(OnMouseDown (MouseClickArgs args)-> )
	(OnMouseUp (MouseClickArgs args)-> )
	(SetLabel (IString label)-> )
	(Enable ->)
	(Disable ->)
)

(class LabelButton
	(implements ILabelButton)
	(implements IEventHandler)
	(Rectf outerRect)
	(Rectf innerRect)
	(Bool isEnabled)
	(IString text)
	(Int32 fontIndex)
)

(method LabelButton.Construct :
	(this.outerRect = -1 -1 -1 -1)
	(this.innerRect = -1 -1 -1 -1)
	(this.isEnabled = true)
	(this.text = "button")
	(this.fontIndex = -1)
)

(factory MHost.Graphics.NewLabelButton ILabelButton : (construct LabelButton))

(method LabelButton.SetRect (IString fontName) (Rectf rect)(IGui g) -> :
	(this.outerRect = rect)
	
	(Float32 dy = (this.outerRect.bottom - this.outerRect.top))
	(Float32 x = (this.outerRect.left + (dy * 0.25)))
	(Float32 x1 = (this.outerRect.right -  (dy * 0.25)))
	(Float32 y0 = (this.outerRect.top + 2))
	(Float32 y1 = (this.outerRect.bottom - 2))
	
	(this.innerRect = x y0 x1 y1)
	
	(GetLargestFontOfFamilyLessThanHeight g fontName dy -> this.fontIndex)
)

(struct ButtonClickArgs
	(IString text)
	(ILabelButton button)
)

(method LabelButton.OnMouseDown (MouseClickArgs args)-> :
	(ButtonClickArgs buttonArgs = this.text this)
	(args.eventHandler.OnButtonDown buttonArgs)
)

(method LabelButton.OnMouseUp (MouseClickArgs args)-> :
	(ButtonClickArgs buttonArgs = this.text this)
	(args.eventHandler.OnButtonUp buttonArgs)
)

(method LabelButton.SetLabel (IString text)-> :
	(this.text = text)
)

(method LabelButton.Enable -> :
	(this.isEnabled = true)
)

(method LabelButton.Disable -> :
	(this.isEnabled = false)
)

(method LabelButton.GetRect (Rectf rect)-> :
	(rect = this.outerRect)
)

(method LabelButton.GetEventHandler -> (IEventHandler handler):
	(handler = this)
)

(method LabelButton.Render (IGui gui)-> :
	(Int32 textColour = (GetTextColour this.outerRect gui))
	(gui.DrawClippedText this.innerRect 0 this.text this.fontIndex textColour this.outerRect)
	(DrawStandardControlBorder gui this.outerRect)
)

(interface MHost.Graphics.IToggleLabel
	(extends MHost.Graphics.IControl)
	(SetRect (IString fontName) (Rectf rect)(IGui g) -> )
	(OnMouseUp (MouseClickArgs args)-> )
	(SetUndefinedText (IString text)-> )
	(AddItem (IString text)(IExpression s)-> )
)

(class ToggleLabel
	(implements IToggleLabel)
	(implements IEventHandler)
	(Rectf outerRect)
	(Rectf innerRect)
	(array ValueCell items)
	(Int32 selection)
	(IString undefinedText)
	(Int32 fontIndex)
)

(method ToggleLabel.Construct (Int32 nItems)->(construct items nItems):
	(this.outerRect = -1 -1 -1 -1)
	(this.selection = -1)
	(this.innerRect = -1 -1 -1 -1)
)

(method ToggleLabel.GetRect (Rectf rect)-> :
	(rect = this.outerRect)
)

(method ToggleLabel.SetRect (IString fontName) (Rectf rect)(IGui g)-> :
	(this.outerRect = rect)
	
	(Float32 dy = (this.outerRect.bottom - this.outerRect.top))
	(Float32 x = (this.outerRect.left + (dy * 0.25)))
	(Float32 x1 = (this.outerRect.right -  (dy * 0.25)))
	(Float32 y0 = (this.outerRect.top + 2))
	(Float32 y1 = (this.outerRect.bottom - 2))
	
	(this.innerRect = x y0 x1 y1)
	
	(GetLargestFontOfFamilyLessThanHeight g fontName dy -> this.fontIndex)
)

(method ToggleLabel.OnMouseUp (MouseClickArgs args)-> :
	(if (args.button == 0)
		(this.selection += 1)
		(Int32 length = this.items.Length)
		(this.selection = (Sys.Maths.I32.Mod this.selection length))
	)
)

(method ToggleLabel.SetUndefinedText (IString text)-> :
	(this.undefinedText = text)
)

(method ToggleLabel.Render (IGui gui)-> :
	(Int32 length = this.items.Length)
	
	(IString text)
	(if ((this.selection >= 0) and (this.selection < length))
		(text = (this.items this.selection text))
	else
		(text = this.undefinedText)
	)
	
	(Int32 textColour = (GetTextColour this.outerRect gui))
	(gui.DrawClippedText this.innerRect (#AlignmentFlagsLeft) text this.fontIndex textColour this.outerRect)
	
	(DrawStandardControlBorder gui this.outerRect)
)

(method ToggleLabel.AddItem (IString text)(IExpression s)-> :
	(ValueCell value = text s)
	(this.items.Push value)
)

(method ToggleLabel.GetEventHandler -> (IEventHandler handler):
	(handler = this)
)

(factory MHost.Graphics.NewToggleLabel MHost.Graphics.IToggleLabel (Int32 nMaxItems): (construct ToggleLabel nMaxItems))

(interface MHost.Graphics.IOverlayControl 
	(extends MHost.Graphics.IControl)
	(SetRect (Rectf rect) -> )
	(SetHost (IEventHandler host) -> )
	(OnMouseUp (MouseClickArgs args)-> )
)

(class Overlay
	(implements IOverlayControl)
	(implements IEventHandler)
	(Rectf outerRect)
	(IEventHandler host)
)

(method Overlay.SetHost (IEventHandler host)-> :
	(this.host = host)
)

(method Overlay.Construct -> :
	(this.outerRect = -1 -1 -1 -1)
)

(method Overlay.GetRect (Rectf rect)-> :
	(rect = this.outerRect)
)

(method Overlay.GetEventHandler -> (IEventHandler handler):
	(handler = this)
)

(method Overlay.OnMouseUp (MouseClickArgs args)-> :
	(this.host.OnMouseUpInOverlay args)
)

(method Overlay.SetRect (Rectf rect)-> :
	(this.outerRect = rect)
)

(struct OverlayProxyArgs
	(IGui g)
	(Rectf rect)
	(IEventHandler overlay)
)

(alias OverlayProxyArgs MHost.OverlayProxyArgs)

(method Overlay.Render (IGui g)-> :
	(OverlayProxyArgs args = g this.outerRect this)
	(this.host.RenderOverlay args)
)

(factory MHost.Graphics.NewOverlay MHost.Graphics.IOverlayControl : (construct Overlay))

(interface MHost.Graphics.IDropValueMenu
	(extends MHost.Graphics.IControl)
	(AddItem (IString text)(IExpression value)->)
	(SetLine (IString fontFamily)(Rectf rect)(IGui g)->)
	(SetUndefinedText (IString text)->)
	(Select (Int32 index)-> )
	(SetOverlay (IOverlayControl overlay)->)
	(RenderOverlay (OverlayProxyArgs args)->)
	(OnMouseUp (MouseClickArgs args)->)
	(OnMouseUpInOverlay (MouseClickArgs args)->)
	(GetValue -> (IExpression s))
)

(struct ValueCell
	(IString text)
	(IExpression value)
)

(class DropValueMenu 
	(implements MHost.Graphics.IDropValueMenu)
	(implements IEventHandler)
	(Rectf outerRect)
	(Rectf innerRect)
	(Rectf textRect)
	(array ValueCell items)
	(Int32 selection)
	(Int32 fontIndex)
	(IOverlayControl overlay)
	(FontDesc fontDesc)
	(Bool isActive)
	(IString undefinedText)
)

(method DropValueMenu.SetUndefinedText (IString text)-> :
	(this.undefinedText = text)
)

(method DropValueMenu.Select (Int32 index)-> :
	(this.selection = index)
)

(method DropValueMenu.SetOverlay (IOverlayControl overlay)-> :
	(this.overlay = overlay)
	(overlay.SetHost this)
	(this.isActive = true)
)

(method DropValueMenu.Construct (Int32 maxItems) -> (construct items maxItems):
	(this.outerRect = -1 -1 -1 -1)
	(this.selection = -1)
	(this.fontIndex = 0)
	(this.isActive = false)
)

(struct ActivateOverlayArgs
	(IDropValueMenu menu)
)

(method DropValueMenu.OnMouseUp (MouseClickArgs mouseClickArgs)-> :
	(if (mouseClickArgs.button == 0)
		(if (this.isActive)
			(this.isActive = false)
		else
			(ActivateOverlayArgs aoArgs)
			(aoArgs.menu = this)
			(mouseClickArgs.eventHandler.ActivateOverlay aoArgs)
		)
	)
)

(function GetTextColour (Rectf rect)(IGui g)-> (Int32 colour):
	(Vec2 cursorPos)
	(g.GetCursorPos cursorPos)
	
	(if (IsPointInRect cursorPos rect)
		(colour = 0xFFFFFFFF)
	else
		(colour = 0xFFC0C0C0)
	)
)

(function DrawSelectedItem (DropValueMenu menu)(IGui g)-> :
	(Int32 length = menu.items.Length)
	
	(IString text)
	(if ((menu.selection < length) and (menu.selection >= 0))
		(text = (menu.items menu.selection text))
	else
		(text = menu.undefinedText)
	)
	
	(Int32 textColour = (GetTextColour menu.outerRect g))
	
	(g.DrawClippedText menu.textRect (#AlignmentFlagsLeft) text menu.fontIndex textColour menu.outerRect)
)

(method DropValueMenu.RenderOverlay (OverlayProxyArgs args)-> :
	(if (this.isActive)
		(args.g.FillRect args.rect 0xFF000030)
		(DrawStandardControlBorder args.g args.rect)
		
		(args.g.FillRect this.innerRect 0xFF000030)
		(DrawSelectedItem this args.g)
	
		(Rectf overlayInnerRect)
		(ShrinkRectf args.rect overlayInnerRect 8)
		
		(DrawStandardControlBorder args.g overlayInnerRect)
		
		(Float32 y = overlayInnerRect.top)
		
		(foreach item # this.items
			(Rectf itemRect)
			(itemRect.left = (overlayInnerRect.left + (this.fontDesc.height * 0.25)))
			(itemRect.right = (overlayInnerRect.right - (this.fontDesc.height * 0.25)))
			(itemRect.top = y)
			
			(Float32 y1 = (y + this.fontDesc.height))
			(itemRect.bottom = y1)
			(y = y1)
			
			(Int32 textColour = (GetTextColour itemRect args.g))
			(args.g.DrawClippedText itemRect (#AlignmentFlagsLeft) item.text this.fontIndex textColour itemRect)
		)
	)
)

(method DropValueMenu.GetRect (Rectf rect)-> :
	(rect = this.outerRect)
)

(method DropValueMenu.GetEventHandler -> (IEventHandler handler):
	(handler = this)
)

(method DropValueMenu.SetLine (IString fontName) (Rectf rect)(IGui g) -> :
	(this.outerRect = rect)
	
	(Float32 dy = (this.outerRect.bottom - this.outerRect.top))
	(GetLargestFontOfFamilyLessThanHeight g fontName dy -> this.fontIndex)
	
	(Float32 x = (this.outerRect.left + 2))
	(Float32 x1 = (this.outerRect.right -  2))
	(Float32 y0 = (this.outerRect.top + 2))
	(Float32 y1 = (this.outerRect.bottom - 2))
	
	(this.innerRect = x y0 x1 y1)
	
	(this.textRect = this.innerRect)
	(this.textRect.left = (this.outerRect.left + (0.25 * dy)))
	
	(IStringBuilder fontName (StringBuilder 64))
	(g.GetFontDescription this.fontIndex fontName this.fontDesc)
)

(method DropValueMenu.OnMouseUpInOverlay (MouseClickArgs args)-> :
	(Rectf overlayRect)
	(this.overlay.GetRect overlayRect)
	
	(Rectf overlayInnerRect)
	(ShrinkRectf overlayRect overlayInnerRect 8)
		
	(Float32 y = overlayInnerRect.top)
		
	(foreach i item # this.items
		(Rectf itemRect)
		(itemRect.left = (overlayInnerRect.left + (this.fontDesc.height * 0.25)))
		(itemRect.right = (overlayInnerRect.right - (this.fontDesc.height * 0.25)))
		(itemRect.top = y)
		
		(Float32 y1 = (y + this.fontDesc.height))
		(itemRect.bottom = y1)
		(y = y1)
		
		(if (IsPointInRect args.pos itemRect)
			(this.selection = i)
			(this.isActive = false)
			(break)
		)
	)
)

(method DropValueMenu.GetValue -> (IExpression s):
	(Int32 length = this.items.Length)
	
	(if ((this.selection < length) and (this.selection >= 0))
		(s = (this.items this.selection value))
	)
)

(method DropValueMenu.Render (IGui g)-> :
	(DrawSelectedItem this g)
	(DrawStandardControlBorder g this.outerRect)
)

(method DropValueMenu.AddItem (IString text)(IExpression value)-> :
	(ValueCell item = text value)
	(this.items.Push item)
)

(factory MHost.Graphics.NewDropValueMenu MHost.Graphics.IDropValueMenu (Int32 maxItems): (construct DropValueMenu maxItems))

(interface RPG.IAddCharacterScreen (extends MHost.IScreen)
	(SetKeyboardFocus (PassEventHandlerArgs peh) -> )
	(ActivateOverlay (ActivateOverlayArgs args) -> )
	(OnButtonUp (ButtonClickArgs args)-> )
)

(class AddCharacterScreen 
	(implements RPG.IAddCharacterScreen)
	(implements IEventHandler)
	
	(IEngine engine) // The CPP side of flow control
	(IUIStack uistack)
	
	(Vec2 screenSpan)
	(Vec2 cursorPos)
	
	(IAttributeControl attributeControl)
	(ILineEditorControl nameEditor)
	(IDropValueMenu classEditor)
	(IEventHandler keyboardSink)
	(IOverlayControl dropOverlay)
	(IToggleLabel sexButton)
	(IDropValueMenu physTraits1)
	(IDropValueMenu physTraits2)
	(IDropValueMenu physTraits3)
	(IDropValueMenu mentalTraits1)
	(IDropValueMenu mentalTraits2)
	(IDropValueMenu mentalTraits3)
	(IDropValueMenu mentalTraits4)
	(IDropValueMenu mentalTraits5)
	(IDropValueMenu profession)
	(IDropValueMenu hobby)
	
	(ILabelButton rollButton)
)

(archetype Sys.Reflection.CallbackWithExpression (IExpression expression) ->)

(function ForDropMenuValue (IDropValueMenu menu) (CallbackWithExpression callback)-> :
	(IExpression s = menu.GetValue)
	(callback s)
)

(function ForEachSelectedDropMenuValue (AddCharacterScreen screen) (CallbackWithExpression callback)-> :
	(ForDropMenuValue screen.physTraits1 callback)
	(ForDropMenuValue screen.physTraits2 callback)
	(ForDropMenuValue screen.physTraits3 callback)
	(ForDropMenuValue screen.mentalTraits1 callback)
	(ForDropMenuValue screen.mentalTraits2 callback)
	(ForDropMenuValue screen.mentalTraits3 callback)
	(ForDropMenuValue screen.mentalTraits4 callback)
	(ForDropMenuValue screen.mentalTraits5 callback)
	(ForDropMenuValue screen.profession callback)
	(ForDropMenuValue screen.hobby callback) 
)

(method AddCharacterScreen.Construct (IEngine engine) -> : 
	(this.engine = engine)
	
	(this.uistack = (MHost.Graphics.NewUIStack))
	(this.screenSpan = -1 -1)
	(this.cursorPos = -1 -1)
	
	(this.attributeControl = (NewAttributeControl))
	(this.uistack.AddTopLevelControl this.attributeControl "attributes")
	
	(this.nameEditor = (NewLineEditor 30))
	(this.uistack.AddTopLevelControl this.nameEditor "name")
	
	(IStringBuilder newName (StringBuilder 128))
	(AppendRandomMaleBritishName newName)
	(this.nameEditor.SetText newName)
	
	(IDropValueMenu classEditor (NewDropValueMenu 7))
	
	// Expression are #ClassQuanitifier #description-strings #starting-currency
	(IExpression classDC = '
		(0 ("The lowest of the low, beggars, thieves and petty criminals. such people are born despised - outcast from"
			"respectable society. There is no background more humble. Almost always homeless. Absolutely no inheritance."
			"Some of the hardest working and most productive of the Human race evolved out of this scum, their energies"
			"channeled to make sure they never return to their roots. If there is one class to betray it is this one!" ) 
			silver: 5 ))
				
	(IExpression classLWC = '
		(1 ("These are what you call working people - the Proletariat. The Great Unwashed. Typically have little more than" 
			"a week's wage in savings. Slum dwellers. Peasants.") 
			silver: 150 ))
				 
	(IExpression classMWC = '
		(2 ("'The salt of the Earth' - Not all working class people are into heavy lifting. There are shopkeepers and smiths,"
			"actors and musicians. Typically have a few month's wages to bequeath to their heirs.") 
			silver: 600))
			
	(IExpression classUWC = '
		(3 ("A class that includes many scientists and engineers and uppity Proles. Very often find themselves outsiders"
		    "as classes above or below will not socialize with them. Generally will leave their heirs with a leg up into the"
			"middle classes") 
			silver: 6000 ))
		
	(IExpression classLMC = '
		(4 ("The lower middle class - or the Borgeous - distinguish themselves by making money from money, and not from hard graft."
			"Paper-pushers, Little-Hitlers abound. Not all middle class people are thieves however... some actually"
			"are the deserving solvent.")
			silver: 15000))

	(IExpression classUMC = '
		(5 ("The upper middle class - solicitors, stockbrokers, tax-collectors and the like. Other than the homeless, they are the group"
			"with the highest incidence of suicide. Souless is often apt. Everyone hates them, except cynics.")
			silver: 40000))
			
	(IExpression classLUC = '
		(6 ("The 'Velvet Mafia' - or the Toff classes - often distinguished by their double-barreled names and family mottos,"
			"they may not have titles, but they do put on the airs of royalty. Almost all of them have government non-jobs,"
			"either that or they 'work' in Law. Behind every great fortune lies a great fraud...")
			silver: 200000))
			
	(IOverlayControl overlay (NewOverlay))
	(this.dropOverlay = overlay)
	(this.uistack.AddTopLevelFloater overlay "dropOverlay")
			
	(classEditor.AddItem "Untouchable" classDC)
	(classEditor.AddItem "Lower Working Class" classLWC)
	(classEditor.AddItem "Middle Working Class" classMWC)
	(classEditor.AddItem "Upper Working Class" classUWC)
	(classEditor.AddItem "Lower Middle Class" classLMC)
	(classEditor.AddItem "Upper Middle Class" classUMC)
	(classEditor.AddItem "Lower Upper Class" classLUC)
	(classEditor.SetUndefinedText "--Social Class--")	
	(this.classEditor = classEditor)
	(this.uistack.AddTopLevelControl this.classEditor "class")
	
	(IToggleLabel sexButton (NewToggleLabel 2))
	
	(IExpression sMale = ' ("male" "man" "boy" "he" "him" "his"))
	(IExpression sFemale = '("female" "woman" "girl" "she" "her" "hers"))
	(sexButton.AddItem "Male" sMale)
	(sexButton.AddItem "Female" sFemale)
	(sexButton.SetUndefinedText "--Sex--")
	
	(this.sexButton = sexButton)
	(this.uistack.AddTopLevelControl this.sexButton "sex")
	
	(IExpression nullExpression = ' ())
	
	(IDropValueMenu physTraits1 (NewDropValueMenu 3))
	(physTraits1.AddItem "Stout" nullExpression)
	(physTraits1.AddItem "Slim" nullExpression)
	(physTraits1.AddItem "Athletic" nullExpression)
	(physTraits1.SetUndefinedText "--Build--")	
	(this.physTraits1 = physTraits1)
	(this.uistack.AddTopLevelControl this.physTraits1 "phy_1")
	
	(IDropValueMenu physTraits2 (NewDropValueMenu 4))
	(physTraits2.AddItem "Giant" nullExpression)
	(physTraits2.AddItem "Tall" nullExpression)
	(physTraits2.AddItem "Short" nullExpression)
	(physTraits2.AddItem "Midget" nullExpression)
	(physTraits2.SetUndefinedText "--Stature--")
	(this.physTraits2 = physTraits2)
	(this.uistack.AddTopLevelControl this.physTraits2 "phy_2")
	
	(IDropValueMenu physTraits3 (NewDropValueMenu 7))
	(physTraits3.AddItem "Bodybuilder" nullExpression)
	(physTraits3.AddItem "Gymnast" nullExpression)
	(physTraits3.AddItem "Pugilist" nullExpression)
	(physTraits3.AddItem "Dancer" nullExpression)
	(physTraits3.AddItem "Cerebral" nullExpression)
	(physTraits3.SetUndefinedText "--Sports--")
	(this.physTraits3 = physTraits3)
	(this.uistack.AddTopLevelControl this.physTraits3 "phy_3")
	
	(IDropValueMenu mentalTraits1 (NewDropValueMenu 4))
	(mentalTraits1.AddItem "Dominant" nullExpression)
	(mentalTraits1.AddItem "Extrovert" nullExpression)
	(mentalTraits1.AddItem "Introvert" nullExpression)
	(mentalTraits1.AddItem "Independent" nullExpression)
	(mentalTraits1.SetUndefinedText "--Persona--")
	(this.mentalTraits1 = mentalTraits1)
	(this.uistack.AddTopLevelControl this.mentalTraits1 "ment_1")
	
	(IDropValueMenu mentalTraits2 (NewDropValueMenu 4))
	(mentalTraits2.AddItem "Miser" nullExpression)
	(mentalTraits2.AddItem "Charitable" nullExpression)
	(mentalTraits2.AddItem "Greedy" nullExpression)
	(mentalTraits2.AddItem "Altruistic" nullExpression)
	(mentalTraits2.SetUndefinedText "--Sociability--")
	(this.mentalTraits2 = mentalTraits2)
	(this.uistack.AddTopLevelControl this.mentalTraits2 "ment_2")
	
	(IDropValueMenu mentalTraits3 (NewDropValueMenu 5))
	(mentalTraits3.AddItem "Stubborn" nullExpression)
	(mentalTraits3.AddItem "Rational" nullExpression)
	(mentalTraits3.AddItem "Spiritual" nullExpression)
	(mentalTraits3.AddItem "Cynical" nullExpression)
	(mentalTraits3.AddItem "Contrarion" nullExpression)
	(mentalTraits3.SetUndefinedText "--Disposition--")
	(this.mentalTraits3 = mentalTraits3)
	(this.uistack.AddTopLevelControl this.mentalTraits3 "ment_3")
	
	(IDropValueMenu mentalTraits4 (NewDropValueMenu 5))
	(mentalTraits4.AddItem "Hedonist" nullExpression)
	(mentalTraits4.AddItem "Egoist" nullExpression)
	(mentalTraits4.AddItem "Gold-digger" nullExpression)
	(mentalTraits4.AddItem "Snob" nullExpression)
	(mentalTraits4.AddItem "Ostentatious" nullExpression)
	(mentalTraits4.SetUndefinedText "--Vice--")
	(this.mentalTraits4 = mentalTraits4)
	(this.uistack.AddTopLevelControl this.mentalTraits4 "ment_4")
	
	(IDropValueMenu mentalTraits5 (NewDropValueMenu 5))
	(mentalTraits5.AddItem "Angry" nullExpression)
	(mentalTraits5.AddItem "Compassionate" nullExpression)
	(mentalTraits5.AddItem "Giddy" nullExpression)
	(mentalTraits5.AddItem "Insouciant" nullExpression)
	(mentalTraits5.AddItem "Serene" nullExpression)
	(mentalTraits5.SetUndefinedText "--Attitude--")
	(this.mentalTraits5 = mentalTraits5)
	(this.uistack.AddTopLevelControl this.mentalTraits5 "ment_5")
	
	(IDropValueMenu profession (NewDropValueMenu 8))
	(profession.AddItem "Soldier" nullExpression)
	(profession.AddItem "Scout" nullExpression)
	(profession.AddItem "Sniper" nullExpression)
	(profession.AddItem "Monk" nullExpression)
	(profession.AddItem "Tactician" nullExpression)
	(profession.SetUndefinedText "--Role--")
	(this.profession = profession)
	(this.uistack.AddTopLevelControl profession "profession")
	
	(IDropValueMenu hobby (NewDropValueMenu 8))
	(hobby.AddItem "Engineer" nullExpression)
	(hobby.AddItem "Linguist" nullExpression)
	(hobby.AddItem "Labourer" nullExpression)
	(hobby.AddItem "Diplomat" nullExpression)
	(hobby.AddItem "Manager" nullExpression)
	(hobby.SetUndefinedText "--Weekend Job--")
	(this.hobby = hobby)
	(this.uistack.AddTopLevelControl hobby "profession")
	
	(ILabelButton rollButton (NewLabelButton))
	(rollButton.SetLabel "Roll!")
	(this.rollButton = rollButton)
	
	(this.uistack.AddTopLevelControl rollButton "roll")
)

(function ApplyModifiers (IAttributeControl control) (IExpression s)-> :
	(Int32 nElements = s.ChildCount)
	(if (nElements > 1)
		(#for (Int32 i = 0)   (i < (nElements - 1))   (i += 1)
			(IExpression child = (s i))
			
			(Int32 type = child.Type)
			(if (type == (#ExpressionTypeAtomic))
				(IString text = child.Text)
			)
		)
	)
)

(method AddCharacterScreen.OnButtonUp (ButtonClickArgs args) -> :
	(if (args.button == this.rollButton)
		(this.attributeControl.SetAttributeByName "Strength" (RollDice 18 10))
		(this.attributeControl.SetAttributeByName "Agility"  (RollDice 18 10))
		(this.attributeControl.SetAttributeByName "Wellness" (RollDice 18 10))
		(this.attributeControl.SetAttributeByName "Psyche"   (RollDice 18 10))
		(this.attributeControl.SetAttributeByName "Ego"      (RollDice 18 10))
		(this.attributeControl.SetAttributeByName "Maths"    (RollDice 18 10))
		
		(IAttributeControl ac = this.attributeControl)
		
		(CallbackWithExpression onValue =
			(closure (IExpression s) -> :
				(ApplyModifiers ac s)
			)
		)
		(ForEachSelectedDropMenuValue this onValue)
	)
)

(method AddCharacterScreen.SetKeyboardFocus (PassEventHandlerArgs args) -> :
	(BoolArgs boolArgs)
	(boolArgs.value = false)
	(boolArgs.source = this)
	(this.keyboardSink.SetHasFocus boolArgs)

	(this.keyboardSink = args.sink)
	
	(boolArgs.value = true)
	(this.keyboardSink.SetHasFocus boolArgs)
)

(method AddCharacterScreen.OnAsciiChar (Int32 asciiValue)-> :
	(KeyboardCharArgs args)
	(args.asciiValue = asciiValue)
	(args.source = this)
	(this.keyboardSink.OnAsciiChar args)
)

(method AddCharacterScreen.OnSysKey (Bool isUp)(Int32 virtualKeyCode)-> :
	(KeyboardVKeyArgs args)
	(args.isUp = isUp)
	(args.virtualKeyCode = virtualKeyCode)
	(args.source = this)
	(this.keyboardSink.OnVirtualKey args)
)

(method AddCharacterScreen.OnMouseDown (Int32 button) (Float32 x)(Float32 y) -> :
	(Vec2 pos = x y)
	
	(IEventHandler evHandler = this)
		
	(OnUIStackItem q = 
		(closure (UIStackItem item)->(Bool terminate):
			(MouseClickArgs args = pos evHandler button)
			(item.handler.OnMouseDown args)
		)
	)
	(this.uistack.RouteMouseEvent pos q)
)

(method AddCharacterScreen.OnMouseUp (Int32 button)(Float32 x)(Float32 y) -> :
	(Vec2 pos = x y)
	
	(if (button == 1)
		(Rectf nameRect)
		(this.nameEditor.GetRect nameRect)
		
		(if (IsPointInRect pos nameRect)
			(IStringBuilder newName (StringBuilder 128))
			(AppendRandomFemaleBritishName newName)
			(this.nameEditor.SetText newName)
		)
	else	
		(IEventHandler evHandler = this)
		
		(OnUIStackItem q = 
			(closure (UIStackItem item)->(Bool terminate):
				(MouseClickArgs args = pos evHandler button)
				(item.handler.OnMouseUp args)
			)
		)
		
		(this.uistack.RouteMouseEvent pos q)
	)
)

(method AddCharacterScreen.OnWheel (Int32 spin) -> :
)

(function Layout (AddCharacterScreen screen)(IGui g) -> :
	(Float32 dy = 8)
	(Rectf attributeRect = 10 dy 250 310)
	(screen.attributeControl.SetRect attributeRect)
	
	(Vec2 span)
	(g.GetScreenSpan span)
	
	(Rectf rollRect)
	(rollRect.left = attributeRect.left)
	(rollRect.top = (attributeRect.bottom + dy))
	(rollRect.right = (rollRect.left +  140))
	(rollRect.bottom = (rollRect.top + 50))
	(screen.rollButton.SetRect "Tahoma" rollRect g)
	
	(Rectf sexRect)
	(sexRect.left = (attributeRect.right + dy))
	(sexRect.top = attributeRect.top)
	(sexRect.right = (sexRect.left +  140))
	(sexRect.bottom = (sexRect.top + 50))
	(screen.sexButton.SetRect "Tahoma" sexRect g)
	
	(Rectf nameRect)
	(nameRect.left = (sexRect.right + dy))
	(nameRect.top = attributeRect.top)
	(nameRect.right = (span.x - 10))
	(nameRect.bottom = (nameRect.top + 50))
	(screen.nameEditor.SetRect "Tahoma" nameRect)
	
	(Rectf overlayRect)
	(overlayRect.left = (span.x - 355))
	(overlayRect.right = (span.x - 5))
	(overlayRect.top = nameRect.bottom)
	(overlayRect.bottom = (span.y - 5))
	
	(screen.dropOverlay.SetRect overlayRect)
	
	(Rectf professionRect)
	(professionRect.left = nameRect.left )
	(professionRect.top = (nameRect.bottom + dy))
	(professionRect.right = (overlayRect.left + 4))
	(professionRect.bottom = (professionRect.top + 50))
	(screen.profession.SetLine "Tahoma" professionRect g)
	
	(Rectf hobbyRect)
	(hobbyRect.left = nameRect.left )
	(hobbyRect.top = (professionRect.bottom + dy))
	(hobbyRect.right = professionRect.right)
	(hobbyRect.bottom = (hobbyRect.top + 50))
	(screen.hobby.SetLine "Tahoma" hobbyRect g)
	
	(Rectf classRect)
	(classRect.left = hobbyRect.left)
	(classRect.top = (hobbyRect.bottom + dy))
	(classRect.right = hobbyRect.right)
	(classRect.bottom = (classRect.top + 50))	
	(screen.classEditor.SetLine "Tahoma" classRect g)
	
	(Rectf phys1Rect)
	(phys1Rect.left = classRect.left)
	(phys1Rect.top = (classRect.bottom + dy))
	(phys1Rect.right = classRect.right)
	(phys1Rect.bottom = (phys1Rect.top + 50))
	(screen.physTraits1.SetLine "Tahoma" phys1Rect g)
	
	(Rectf phys2Rect)
	(phys2Rect.left = phys1Rect.left)
	(phys2Rect.top = (phys1Rect.bottom + dy))
	(phys2Rect.right = phys1Rect.right)
	(phys2Rect.bottom = (phys2Rect.top + 50))
	(screen.physTraits2.SetLine "Tahoma" phys2Rect g)
	
	(Rectf phys3Rect)
	(phys3Rect.left = phys2Rect.left)
	(phys3Rect.top = (phys2Rect.bottom + dy))
	(phys3Rect.right = phys2Rect.right)
	(phys3Rect.bottom = (phys3Rect.top + 50))
	(screen.physTraits3.SetLine "Tahoma" phys3Rect g)
	
	(Rectf mentalTraits1Rect)
	(mentalTraits1Rect.left = phys3Rect.left)
	(mentalTraits1Rect.top = (phys3Rect.bottom + dy))
	(mentalTraits1Rect.right = phys3Rect.right)
	(mentalTraits1Rect.bottom = (mentalTraits1Rect.top + 50))
	(screen.mentalTraits1.SetLine "Tahoma" mentalTraits1Rect g)
	
	(Rectf mentalTraits2Rect)
	(mentalTraits2Rect.left = mentalTraits1Rect.left)
	(mentalTraits2Rect.top = (mentalTraits1Rect.bottom + dy))
	(mentalTraits2Rect.right = mentalTraits1Rect.right)
	(mentalTraits2Rect.bottom = (mentalTraits2Rect.top + 50))
	(screen.mentalTraits2.SetLine "Tahoma" mentalTraits2Rect g)
	
	(Rectf mentalTraits3Rect)
	(mentalTraits3Rect.left = mentalTraits2Rect.left)
	(mentalTraits3Rect.top = (mentalTraits2Rect.bottom + dy))
	(mentalTraits3Rect.right = mentalTraits2Rect.right)
	(mentalTraits3Rect.bottom = (mentalTraits3Rect.top + 50))
	(screen.mentalTraits3.SetLine "Tahoma" mentalTraits3Rect g)
	
	(Rectf mentalTraits4Rect)
	(mentalTraits4Rect.left = mentalTraits3Rect.left)
	(mentalTraits4Rect.top = (mentalTraits3Rect.bottom + dy))
	(mentalTraits4Rect.right = mentalTraits3Rect.right)
	(mentalTraits4Rect.bottom = (mentalTraits4Rect.top + 50))
	(screen.mentalTraits4.SetLine "Tahoma" mentalTraits4Rect g)
	
	(Rectf mentalTraits5Rect)
	(mentalTraits5Rect.left = mentalTraits4Rect.left)
	(mentalTraits5Rect.top = (mentalTraits4Rect.bottom + dy))
	(mentalTraits5Rect.right = mentalTraits4Rect.right)
	(mentalTraits5Rect.bottom = (mentalTraits5Rect.top + 50))
	(screen.mentalTraits5.SetLine "Tahoma" mentalTraits5Rect g)
)

(method AddCharacterScreen.RenderGui (IGui g) -> :
	(Vec2 screenSpan)
	(g.GetScreenSpan screenSpan)
	
	(Rectf focusRect = -1 -1 -1 -1)
	(this.keyboardSink.GetRect focusRect)
	
	(if (focusRect.left != -1)
		(Rectf renderFocusRect)
		(ShrinkRectf focusRect renderFocusRect -2)
		(g.DrawBorder focusRect 2 0xFFFFFFFF 0xFFA0A0A0 0xFF808080 0xFF606060)
	)
	
	(if (screenSpan != this.screenSpan)
		(this.screenSpan = screenSpan)
		(Layout this g)
	)
	
	(Rectf nameRect)
	(this.nameEditor.GetRect nameRect)
	
	(Rectf toolTip = nameRect)
	(toolTip.right -= 4)
	(toolTip.bottom -= 4)
	(Int32 align = ((#AlignmentFlagsRight) + (#AlignmentFlagsBottom)))
	(g.DrawText toolTip align "right-click *here* to generate a random name" 5 0xFFC0C0C0)
	
	(RenderTree this.uistack g)
)

(method AddCharacterScreen.ActivateOverlay (ActivateOverlayArgs args)-> :
	(args.menu.SetOverlay this.dropOverlay)
)

(factory RPG.NewAddCharacterScreen IScreen (IEngine engine) : 
	(construct AddCharacterScreen engine)
)
