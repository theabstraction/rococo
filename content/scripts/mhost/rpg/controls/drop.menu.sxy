(using Sys)
(using Sys.Type)
(using Sys.Maths)
(using Sys.Random)
(using Sys.Maths.F32)
(using Sys.Reflection)
(using Sys.Type.Strings)

(using MPlat)
(using MHost)
(using MHost.Events)
(using MHost.OS)
(using MHost.Graphics)
(using MHost.Graphics.Utils)
(using MHost.Graphics.Widgets)
(using MHost.Graphics.Drawing2D)
(using Rococo)
(using Rococo.Graphics)
(using Rococo.Audio)

(interface MHost.Graphics.IOverlayControl 
	(extends MHost.Graphics.Widgets.IControl)
	(SetRect (Rectf rect) -> )
	(SetHost (IEventHandler host) -> )
)

(class Overlay
	(implements IOverlayControl)
	(implements IEventHandler)
	(Rectf outerRect)
	(IEventHandler host)
)

(method Overlay.SetHost (IEventHandler host)-> :
	(this.host = host)
)

(method Overlay.Construct -> :
	(this.outerRect = -1 -1 -1 -1)
)

(method Overlay.GetRect (Rectf rect)-> :
	(rect = this.outerRect)
)

(method Overlay.GetEventHandler -> (IEventHandler handler):
	(handler = this)
)

(method Overlay.OnMouseUp (MouseClickArgs args)-> :
	(this.host.OnMouseUpInOverlay args)
)

(method Overlay.SetRect (Rectf rect)-> :
	(this.outerRect = rect)
)

(struct OverlayProxyArgs
	(IGui g)
	(Rectf rect)
	(IEventHandler overlay)
)

(alias OverlayProxyArgs MHost.OverlayProxyArgs)

(method Overlay.Render (IGui g)-> :
	(OverlayProxyArgs args = g this.outerRect this)
	(this.host.RenderOverlay args)
)

(factory MHost.Graphics.NewOverlay MHost.Graphics.IOverlayControl : (construct Overlay))

(struct SelectionChangedArgs
	(IEventHandler control)
)

(alias SelectionChangedArgs MHost.Graphics.SelectionChangedArgs)

(interface MHost.Graphics.IDropValueMenu
	(extends MHost.Graphics.Widgets.IControl)
	(AddItem (IString text)(IExpression value)->)
	(SetLine (Rectf rect)(IGui g)->)
	(SetUndefinedText (IString text)->)
	(Select (Int32 index)-> )
	(SetOverlay (IOverlayControl overlay)->)
	(GetValue -> (IExpression s))
	(Enable -> )
	(Disable -> )
	(DisableGlow ->)
	(EnableGlow ->)
	(GetSelection -> (Int32 index))
)

(class DropValueMenu 
	(implements MHost.Graphics.IDropValueMenu)
	(implements IEventHandler)
	(Rectf outerRect)
	(Rectf innerRect)
	(Rectf textRect)
	(array ValueCell items)
	(Int32 selection)
	(Int32 fontIndex)
	(IOverlayControl overlay)
	(Float32 fontHeight)
	(Bool isActive)
	(IString undefinedText)
	(Bool isEnabled)
	(Bool isGlowing)
	(IEventHandler subscriber)
)

(method DropValueMenu.GetSelection -> (Int32 index) : (index = this.selection))
(method DropValueMenu.DisableGlow -> : (this.isGlowing = false))
(method DropValueMenu.EnableGlow -> : (this.isGlowing = true))

(method DropValueMenu.SetUndefinedText (IString text)-> :
	(this.undefinedText = text)
)

(method DropValueMenu.Select (Int32 index)-> :
	(this.selection = index)
)

(method DropValueMenu.SetOverlay (IOverlayControl overlay)-> :
	(this.overlay = overlay)
	(overlay.SetHost this)
	(this.isActive = true)
)

(method DropValueMenu.Construct (Int32 maxItems) (IEventHandler subscriber):
	(array ValueCell items maxItems)
	(this.items = items)
	(this.outerRect = -1 -1 -1 -1)
	(this.selection = -1)
	(this.fontIndex = 0)
	(this.fontHeight = 32)
	(this.isActive = false)
	(this.isEnabled = true)
	(this.isGlowing = false)
	(this.subscriber = subscriber)
)

(method DropValueMenu.Disable -> :
	(this.isEnabled = false)
)

(method DropValueMenu.Enable -> :
	(this.isEnabled = true)
)

(struct ActivateOverlayArgs
	(IDropValueMenu menu)
)

(alias ActivateOverlayArgs MHost.Graphics.ActivateOverlayArgs)

(method DropValueMenu.OnMouseUp (MouseClickArgs mouseClickArgs)-> :
	(if (this.isEnabled)
		(if (mouseClickArgs.button == 0)
			(if (this.isActive)
				(this.isActive = false)
			else
				(ActivateOverlayArgs aoArgs)
				(aoArgs.menu = this)
				(mouseClickArgs.eventHandler.ActivateOverlay aoArgs)
			)
		)
	)
)

(method DropValueMenu.DrawSelectedItem (IGui g)(Bool isEnabled)-> :
	(Int32 length = this.items.Length)
	
	(IString text)
	(if ((this.selection < length) and (this.selection >= 0))
		(text = (this.items this.selection text))
	else
		(text = this.undefinedText)
	)
	
	(Int32 textColour = (GetTextColourAtRectUnderCursor this.outerRect g this.isEnabled))
	
	(g.DrawClippedText this.textRect (#AlignmentFlagsLeft) text this.fontIndex textColour this.outerRect)
)

(method DropValueMenu.RenderOverlay (OverlayProxyArgs args)-> :
	(if (this.isActive)
		(args.g.FillRect args.rect 0xFF000030)
		(DrawStandardControlBorder args.g args.rect this.isEnabled this.isGlowing)
		
		(args.g.FillRect this.innerRect 0xFF000030)
		(this.DrawSelectedItem args.g this.isEnabled)
	
		(Rectf overlayInnerRect)
		(ShrinkRectf args.rect overlayInnerRect 8)
		
		(DrawStandardControlBorder args.g overlayInnerRect this.isEnabled false)
		
		(Float32 y = overlayInnerRect.top)
		
		(foreach item # this.items
			(Rectf itemRect)
			(itemRect.left = (overlayInnerRect.left + (this.fontHeight * 0.25)))
			(itemRect.right = (overlayInnerRect.right - (this.fontHeight * 0.25)))
			(itemRect.top = y)
			
			(Float32 y1 = (y + this.fontHeight))
			(itemRect.bottom = y1)
			(y = y1)
			
			(Int32 textColour = (GetTextColourAtRectUnderCursor itemRect args.g this.isEnabled))
			(args.g.DrawText itemRect (#AlignmentFlagsLeft) item.text 0 textColour)
		)
	)
)

(method DropValueMenu.GetRect (Rectf rect)-> :
	(rect = this.outerRect)
)

(method DropValueMenu.GetEventHandler -> (IEventHandler handler):
	(handler = this)
)

(method DropValueMenu.SetLine (Rectf rect)(IGui g) -> :
	(this.outerRect = rect)
	
	(Float32 dy = (this.outerRect.bottom - this.outerRect.top))
	
	(Float32 x = (this.outerRect.left + 2))
	(Float32 x1 = (this.outerRect.right -  2))
	(Float32 y0 = (this.outerRect.top + 2))
	(Float32 y1 = (this.outerRect.bottom - 2))
	
	(this.innerRect = x y0 x1 y1)
	
	(this.textRect = this.innerRect)
	(this.textRect.left = (this.outerRect.left + (0.25 * dy)))
)

(method DropValueMenu.OnMouseUpInOverlay (MouseClickArgs args)-> :
	(Rectf overlayRect)
	(this.overlay.GetRect overlayRect)
	
	(Rectf overlayInnerRect)
	(ShrinkRectf overlayRect overlayInnerRect 8)
		
	(Float32 y = overlayInnerRect.top)
		
	(Int32 i = 0)
	(foreach item # this.items
		(Rectf itemRect)
		(itemRect.left = (overlayInnerRect.left + (this.fontHeight * 0.25)))
		(itemRect.right = (overlayInnerRect.right - (this.fontHeight * 0.25)))
		(itemRect.top = y)
		
		(Float32 y1 = (y + this.fontHeight))
		(itemRect.bottom = y1)
		(y = y1)
		
		(if (IsPointInRect args.pos itemRect)
			(this.selection = i)
			(this.isActive = false)
			
			(SelectionChangedArgs oscArgs)
			(oscArgs.control = this)
			(this.subscriber.OnSelectionChanged oscArgs)
			(break)
		)
		
		(i += 1)
	)
)

(method DropValueMenu.GetValue -> (IExpression s):
	(Int32 length = this.items.Length)
	
	(if ((this.selection < length) and (this.selection >= 0))
		(s = (this.items this.selection value))
	)
)

(method DropValueMenu.Render (IGui g)-> :
	(this.DrawSelectedItem g this.isEnabled)
	(DrawStandardControlBorder g this.outerRect this.isEnabled this.isGlowing)
)

(method DropValueMenu.AddItem (IString text)(IExpression value)-> :
	(ValueCell item = text value)
	(this.items.Push item)
)

(factory MHost.Graphics.NewDropValueMenu MHost.Graphics.IDropValueMenu (Int32 maxItems)(IEventHandler subscriber):
	(construct DropValueMenu maxItems subscriber)
)