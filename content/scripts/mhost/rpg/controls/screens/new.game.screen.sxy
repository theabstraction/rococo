(using Sys)
(using Sys.Maths)
(using Sys.Maths.F32)
(using Sys.Type)
(using Sys.Random)
(using Sys.Type.Strings)
(using Sys.Reflection)

(using MPlat)
(using MHost)
(using MHost.Events)
(using MHost.OS)
(using MHost.Graphics)
(using MHost.Graphics.Utils)
(using MHost.Graphics.Drawing2D)
(using MHost.Graphics.Widgets)
(using Rococo)
(using Rococo.Graphics)
(using Rococo.Audio)
(using RPG)

(struct Slot
	(Rectf rect)
	(ICharDataStore charData)
)

(class NewGameScreen 
	(implements IScreen)
	(implements IEventHandler)
	
	(IBundle bundle) 
	(IUIStack uistack)
	
	(Vec2 screenSpan)
	(Vec2 cursorPos)
	
	(array Slot slots) 

	(IListBuilder roster)
	
	(IListRow selectedRow)
	
	(ILabelButton cancelButton)
	(ILabelButton newGameButton)
	
	(Rectf hintRect)
)

(struct CharRow
	(IString name)
	(IString portrait)
	(Int32 strength)
	(Int32 agility)
	(Int32 wellness)
	(Int32 psyche)
	(Int32 ego)
	(Int32 maths)
)

(interface RPG.ICharDataStore
	(GetCharData (CharRow data)->)
	(AssignToSlot (Int32 index) ->)
)

(class PlayerRow
	(implements IListRow)
	(implements ICharDataStore)
	(CharRow data)
	(IEngine engine)
	(Int32 slot)
)

(method PlayerRow.GetCharData (CharRow data)-> :
	(data = this.data)
)

(method PlayerRow.Render (IGui g)(Rectf rect)-> :
	(g.DrawBorder rect 4 0xFFFFFFFF 0xFFAFAFAF 0xFF8F8F8F 0xFF5F5F5F)
	
	(Rectf faceRect)
	(faceRect.left = rect.left)
	(faceRect.right = (rect.left + (rect.bottom - rect.top)))
	(faceRect.top = rect.top)
	(faceRect.bottom = rect.bottom)
	
	(Rectf nameRect)
	(nameRect.left = (faceRect.right + 1))
	(nameRect.right = rect.right)
	(nameRect.top = rect.top)
	(nameRect.bottom = rect.bottom)
	
	(g.DrawText nameRect 0 this.data.name 0 0xFFFFFFFF)
	
	(MPlat.BitmapLocation img)
	(this.engine.GetSpriteSpec this.data.portrait img)
	
	(if (img.arrayIndex >= 0) 
		(Rectf innerFaceRect)
		(innerFaceRect.left = (faceRect.left + 4))
		(innerFaceRect.top = (faceRect.top + 4))
		(innerFaceRect.right = (faceRect.right - 4))
		(innerFaceRect.bottom = (faceRect.bottom - 4))
		(g.StretchSprite innerFaceRect img)
	)
	
	(if (this.slot >= -1)
		(g.DrawBorder rect 4 0xFFFFFFFF 0xFFAFAFAF 0xFF8F8F8F 0xFF5F5F5F)
		(g.FillRect rect 0xC0404040)
	)
)

(method PlayerRow.GetSpan (Vec2 span)-> :
	(span = 200 64)
)

(method PlayerRow.Construct (CharRow data)(IEngine engine): 
	(this.data = data)
	(this.engine = engine)
	(this.slot = -2) // -2 = none, -1 = cursor, 0+ = party slot
)

(method PlayerRow.AssignToSlot (Int32 slotId)-> :
	(this.slot = slotId)
)

(factory RPG.NewPlayerRow IListRow (CharRow data)(IEngine engine): (construct PlayerRow data engine))

(function ThrowSourceError (IString message)(IString source)-> :
	(IStringBuilder sb = (NewParagraphBuilder))
	(#build sb message ": " source)
	(Sys.Throw 0 sb)
)

(function IsAtomic (IExpression s)-> (Bool isAtomic):
	(Int32 typeAtomic = (#ExpressionTypeAtomic))
	(isAtomic = (s.Type == typeAtomic))
)

(function AssertAtomic (IExpression s)(IString source)-> :
	(if (not (IsAtomic s))
		(ThrowSourceError "Expecting atomic expression" source)
	)
)

(function AssertExpressionIsAtomicString (IExpression s)(IString value)(IString source)-> :
	(AssertAtomic s source)
		
	(Bool isMatched = (Sys.Type.Strings.IsExactMatch value s.Text))
	
	(if (not isMatched)
		(IStringBuilder sb = (NewParagraphBuilder))
		(#build sb "Expecting <" value ">: " source)
		(Sys.Throw 0 sb)
	)
)

(function AssertSFormatDictionary (IExpression s)(IString source)-> :
	(IExpression sRoot = (s 0))
	(Int32 childCount = (sRoot.ChildCount))
	(if (childCount != 3)
		(ThrowSourceError "Expecting 3 top level elements in an S-Format dictionary" source)
	)
	
	(IExpression sQuote = (sRoot 0))
	(AssertExpressionIsAtomicString sQuote "'" source)
	
	(IExpression sID = (sRoot 1))
	(AssertExpressionIsAtomicString sID "S-Format-Dictionary-v1.0" source)
)

(function AddCharRow (IListBuilder roster) (IString filename)(IEngine engine)-> :
	(CharRow data)
	(IExpression s = (MHost.OS.LoadExpression filename))
	
	/* Example expression:
	
	(' S-Format-Dictionary-v1.0 
		( 
			(I32 Strength 135)
			(I32 Agility 146)
			(I32 Wellness 146)
			(I32 Psyche 89)
			(I32 Ego 110)
			(I32 Maths 130)
			(STR Name "Corbin Fosnought")
		)
	)
	
	*/
	
	(AssertSFormatDictionary s filename)
	
	(IExpression sRoot = (s 0))
	(IExpression sAttributes = (sRoot 2))
	(#for (Int32 i = 0) (i < sAttributes.ChildCount)(i += 1)
		(IExpression sAttribute = (sAttributes i))
		(serialize sAttribute -> data)	
	)
	
	(IListRow row1 (NewPlayerRow data engine))
	(roster.AddRow row1)
)

(method NewGameScreen.Construct (IBundle bundle) -> (construct slots 5): 
	(this.bundle = bundle)
	(this.uistack = (MHost.Graphics.NewUIStack))
	(this.screenSpan = -1 -1)
	(this.cursorPos = -1 -1)
	(this.roster = (NewListBuilder bundle.UIControl 100))
	(this.uistack.AddTopLevelControl this.roster "roster")
	
	(IListBuilder roster = this.roster)
	(IEngine engine = this.bundle.Engine)
	
	(MPlat.OnFileName appendToList =
		(closure (IString fileName) -> :
			(AddCharRow roster fileName engine)
		)
	)
	
	(MPlat.OS.EnumerateFiles "!scripts/mhost/rpg/chaps/*.dict" appendToList)
	
	(ILabelButton cancelButton (NewLabelButton))
	(cancelButton.SetLabel "Cancel")
	(this.cancelButton = cancelButton)
	(this.uistack.AddTopLevelControl cancelButton "cancel")
	
	(ILabelButton newGameButton (NewLabelButton))
	(newGameButton.SetLabel "Enter World...")
	(this.newGameButton = newGameButton)
	(this.uistack.AddTopLevelControl newGameButton "newGame")
	
	(this.SyncUI)
)

(method NewGameScreen.OnClickListRow (SelectListRowArgs args) -> :
	(if args.withMouseDown
		(this.selectedRow = args.selectedRow)
		(cast this.selectedRow -> ICharDataStore cds)
		(cds.AssignToSlot -1)
	)
)

(factory RPG.NewNewGameScreen MHost.Graphics.IScreen (IBundle bundle):
	(construct NewGameScreen bundle)
)

(method NewGameScreen.Layout (IGui g) -> :
	(Vec2 ds)
	(g.GetScreenSpan ds)
	
	(Vec2 portraitSpan = 80 80)
	(Float32 border = 10)
	
	(Rectf slot0)
	(slot0.left = border)
	(slot0.top = border)
	(slot0.right = (border + portraitSpan.x))
	(slot0.bottom = (border + portraitSpan.y))

	(Rectf slot1 = slot0)
	(slot1.left = (slot0.right + border))
	(slot1.right = (slot1.left + portraitSpan.x))

	(Rectf slot2 = slot0)
	(slot2.left = (slot1.right + border))
	(slot2.right = (slot2.left + portraitSpan.x))

	(Rectf slot3 = slot0)
	(slot3.top = (slot0.bottom + border))
	(slot3.bottom = (slot3.top + portraitSpan.y))

	(Rectf slot4 = slot2)
	(slot4.top = slot3.top)
	(slot4.bottom = slot3.bottom)
	
	(this.slots.Clear)
	(ICharDataStore nullChar)
	(this.slots.Push Slot (slot0 nullChar))
	(this.slots.Push Slot (slot1 nullChar))
	(this.slots.Push Slot (slot2 nullChar))
	(this.slots.Push Slot (slot3 nullChar))
	(this.slots.Push Slot (slot4 nullChar))
	
	(Rectf rosterRect)
	(rosterRect.left = (slot2.right + border))
	(rosterRect.top = border)
	(rosterRect.right = (ds.x - border))
	(rosterRect.bottom = (ds.y - 160))
	
	(this.roster.SetRect rosterRect g)

	(Float32 centreX = (0.5 * ds.x))
	
	(Rectf cancelRect)
	(cancelRect.bottom = (ds.y - 10))
	(cancelRect.top = (cancelRect.bottom - 40))
	(cancelRect.left = (centreX - 70))
	(cancelRect.right = (centreX + 70))
	
	(this.cancelButton.SetRect "Tahoma" cancelRect g)
	
	(Rectf newGameRect)
	(newGameRect.bottom = (cancelRect.top - 20))
	(newGameRect.top = (newGameRect.bottom - 40))
	(newGameRect.left = (centreX - 112))
	(newGameRect.right = (centreX + 112))
	
	(this.newGameButton.SetRect "Tahoma" newGameRect g)
	
	(
		(Float32 right = (this.screenSpan.x - 5))
		(Float32 top = (rosterRect.bottom + border))
		(Float32 bottom = (top + 34))
		(this.hintRect = 5 top right bottom)
	)
)

(method NewGameScreen.RenderGui (IGui g) -> :
	(Vec2 screenSpan)
	(g.GetScreenSpan screenSpan)
	
	(if (screenSpan != this.screenSpan)
		(this.screenSpan = screenSpan)
		(this.Layout g)
	)
	
	(Rectf rect = 0 0 this.screenSpan.x this.screenSpan.y)
	(g.DrawBorder rect 4 0xFFFFFFFF 0xFFAFAFAF 0xFF8F8F8F 0xFF5F5F5F)
	
	(g.DrawText this.hintRect 0 "Drag and drop portraits to character slots to build a party." 2 0xFFFFFFFF)
	
	(RenderTree this.uistack g)
	
	(foreach slot # this.slots
		(g.DrawBorder slot.rect 1 0xFFFFFFFF 0xFFAFAFAF 0xFF8F8F8F 0xFF5F5F5F)
		
		(if (slot.charData ?)
			(CharRow charData)
			(slot.charData.GetCharData charData)
			(IEngine engine = this.bundle.Engine)
			
			(MPlat.BitmapLocation img)
			(if (charData.portrait.Length > 0)
				(engine.GetSpriteSpec charData.portrait img)
				(g.StretchSprite slot.rect img)
			)
		)
	)
	
	(if (this.selectedRow ?)
		(cast this.selectedRow -> ICharDataStore cds)
		(CharRow charData)
		(cds.GetCharData charData)
		(IEngine engine = this.bundle.Engine)
		
		(MPlat.BitmapLocation img)
		(if (charData.portrait.Length > 0)
			(engine.GetSpriteSpec charData.portrait img)
			
			(Vec2 cursorPos)
			(g.GetCursorPos cursorPos)
			
			(Vec2 dragSpan = 56 56)
			
			(Rectf dragRect)
			(dragRect.left = cursorPos.x)
			(dragRect.top = cursorPos.y)
			(dragRect.right = (cursorPos.x + dragSpan.x))
			(dragRect.bottom = (cursorPos.y + dragSpan.y))
			
			(g.StretchSprite dragRect img)
		)
	)
)

(method NewGameScreen.SyncUI -> :
	// Make the Enter World button invisible if no slot is filled
	(this.uistack.SetVisible this.newGameButton false)
	
	(foreach s # this.slots
		(if (s.charData ?)
			(this.uistack.SetVisible this.newGameButton true)
			(this.newGameButton.EnableGlow)
		)
	)
)

(method NewGameScreen.OnMouseDown (Int32 button) (Float32 x)(Float32 y) -> :
	(Vec2 pos = x y)
	
	// Remove portrait if it is under the cursor
	(foreach slot # this.slots
		(if (IsPointInRect pos slot.rect)
			(slot.charData.AssignToSlot -2)
			(slot.charData = 0)
			
			(this.SyncUI)
			(return)
		)
	)
	
	(IEventHandler evHandler = this)
		
	(OnUIStackItem q = 
		(closure (UIStackItem item)->(Bool terminate):
			(MouseClickArgs args = pos evHandler button)
			(item.handler.OnMouseDown args)
		)
	)
	(this.uistack.RouteMouseEvent pos q)
)

(method NewGameScreen.OnMouseUp (Int32 button)(Float32 x)(Float32 y) -> :
	(Vec2 pos = x y)
	
	(IEventHandler evHandler = this)
	
	(cast this.selectedRow -> ICharDataStore cds)
	(cds.AssignToSlot -2)
	
	// Find which slot is under the cursor and set it to the selected row,
	// and remove all references to the selected row in the remaining slots
	(foreach i slot # this.slots
		(if (IsPointInRect pos slot.rect)
			(cds.AssignToSlot i)
			(slot.charData.AssignToSlot -2) // this tells the character at the slot it is no longer assigned
			(slot.charData = cds)
			
			(foreach j slotJ # this.slots
				(if (i != j)
					(if (slotJ.charData ==  cds)
						(slotJ.charData = 0)
					)
				)
			)

			(this.SyncUI)
			(break) // Allow execution to return to the uistack as we need to tell the drag and drop source the drop occured
		)
	)
	
	(this.selectedRow = 0)
	
	(OnUIStackItem q = 
		(closure (UIStackItem item)->(Bool terminate):
			(MouseClickArgs args = pos evHandler button)
			(item.handler.OnMouseUp args)
		)
	)
	
	(this.uistack.RouteMouseEvent pos q)
)

(method NewGameScreen.OnMouseMove (Float32 x)(Float32 y) -> :
	(Vec2 pos = x y)
	
	(IEventHandler evHandler = this)
	
	(OnUIStackItem q = 
		(closure (UIStackItem item)->(Bool terminate):
			(MouseClickArgs args = pos evHandler 0)
			(item.handler.OnMouseMove args)
		)
	)
	
	(this.uistack.RouteMouseEvent pos q)
)

(method NewGameScreen.OnWheel (Int32 spin) -> :
)

(method NewGameScreen.OnAsciiChar (Int32 asciiValue)-> :
)

(method NewGameScreen.OnSysKey (Bool isUp)(Int32 virtualKeyCode)-> :
	(this.uistack.RouteSysKey isUp virtualKeyCode)
)

(method NewGameScreen.OnButtonUp (ButtonClickArgs args) -> :
	(if (args.button == this.cancelButton)
		(this.bundle.SetActiveScreen (#IdScreenOpening))
	)
	
	(if (args.button == this.newGameButton)
		(this.bundle.SetActiveScreen (#IdScreenWorld))
	)
)
