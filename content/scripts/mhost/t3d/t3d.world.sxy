// 3d test

(' #include 
	"!scripts/mhost/mplat.types.sxy"
	"!scripts/mhost/mhost.sxh.sxy"
	"!scripts/mplat.sxh.sxy"
	"!scripts/types.sxy"
	"!scripts/mhost/mhost.include.sxy"
	"!scripts/mhost/widgets/widgets.sxy"
)

(using Sys)
(using Sys.Maths)
(using Sys.Maths.F32)
(using Sys.Type)
(using Sys.Random)
(using Sys.Type.Strings)

(using MPlat)
(using MHost)
(using MHost.OS)
(using MHost.Graphics)
(using Rococo)
(using Rococo.Graphics)
(using Rococo.Audio)
(using Rococo.Entities)

(using Sys.Maths.I32)

(namespace T3D)
(using T3D)

(class World (implements T3D.IWorld)
	(Vec3 cameraPos)
	(WorldOrientation cameraOrientation)
	(IMeshBuilder meshBuilder)
)

(method World.UpdateMouseLook (Vec2 delta)-> :
	(Float32 newBearing = (delta.x + this.cameraOrientation.bearing))
	(Float32 newElevation = (delta.y + this.cameraOrientation.elevation))
	(this.cameraOrientation.bearing = (Sys.Maths.F32.Mod newBearing 360))
	(this.cameraOrientation.elevation = (Sys.Maths.F32.Clamp newElevation -45 45))
)

(method World.GetCameraOrientation (WorldOrientation cameraOrientation)-> :
	(cameraOrientation = this.cameraOrientation)
)

(function MakeBottle (IString meshName) -> :
	(IRodTesselator bottle (RodTesselator))
	(bottle.Clear)
	
	(IInstances instances (Instances))
	
	(MaterialVertexData glass)
	(AnyColour 0 255 0 255 0 255 48 224 -> glass.colour)
	(instances.GetRandomMaterialId (#MaterialCategoryRock) -> glass.id)
	(glass.gloss = 0.2)
	
	(bottle.SetMaterialTop glass)
	(bottle.SetMaterialMiddle glass)
	(bottle.SetMaterialBottom glass)
	
	(Float32 length = (AnyFloat 0.05 0.25))
	(Float32 bottomWidth = (AnyFloat 0.025 0.10))
	(Float32 topWidth = (bottomWidth * (AnyFloat 0.5 1)))
	(Int32 divs = (AnyInt 4 8))
	(bottle.AddTube length bottomWidth topWidth divs)
	
	(Float32 neckLength = (AnyFloat 0.01 0.05))
	(bottle.AddTube neckLength topWidth 0.01 divs)
	
	(MaterialVertexData stopper)
	(instances.GetRandomMaterialId (#MaterialCategoryRock) -> stopper.id)
	(stopper.gloss = 0.2)
	(AnyColour 0 255 0 255 0 255 48 128 -> stopper.colour)
	(bottle.SetMaterialTop stopper)
	(bottle.SetMaterialMiddle stopper)
	(bottle.SetMaterialBottom stopper)
		
	(Float32 stopperLength = (AnyFloat 0.005 0.02))
	(Int32 stopperDivs = (AnyInt 4 6))
	(bottle.AddTube stopperLength (AnyFloat 0.01 0.02) (AnyFloat 0.01 0.035) stopperDivs)
	
	(bottle.CopyToMeshBuilder meshName false false true)
)

(function MakeFloor (IString meshName) -> :
	(IRodTesselator rod (RodTesselator))
	(rod.Clear)
	
	(IInstances instances (Instances))
	
	(MaterialVertexData marble)
	(AnyColour 200 255 0 0 0 0 192 192 -> marble.colour)
	(instances.GetRandomMaterialId (#MaterialCategoryMarble) -> marble.id)
	(marble.gloss = 0.1)
	
	(rod.SetMaterialTop marble)
	(rod.SetMaterialMiddle marble)
	(rod.SetMaterialBottom marble)
	
	(rod.SetUVScale 0.05)
	
	(Vec2 b0 = -10  10)
	(Vec2 b1 =  10  10)
	(Vec2 b2 =  10 -10)
	(Vec2 b3 = -10 -10)
	(rod.AddBox 0.25 b0 b1 b2 b3)
	
	(rod.CopyToMeshBuilder meshName false false true)
)

(method World.Construct : 
	(this.cameraPos = 0 0 1)
	(this.cameraOrientation = 0 0 0)
	
	(IInstances instances (Instances))
	(instances.Clear)
	(instances.LoadMaterialArray "!textures/hv/materials/hi-rez/" 1024) 
	
//	(MakeBottle "potion.1") 
	
	(Matrix4x4 model = 
		(1 0 0 0)
		(0 1 0 1)
		(0 0 1 0) 
		(0 0 0 1)
	)
	
	(Vec3 noScale = 1 1 1)
	
//	(Int64 bottleId = (instances.AddBody "potion.1" model noScale 0))
	
	(MakeFloor "floor.1")
	
	(Matrix4x4 model2 = 
		(1 0 0    0)
		(0 1 0    1)
		(0 0 1 -1.0) 
		(0 0 0    1)
	)
	
	(Int64 floorId = (instances.AddBody "floor.1" model2 noScale 0))
	
	(ISceneBuilder scene (SceneBuilder))
	(scene.Clear)
//	(scene.AddStatics bottleId)
	(scene.AddStatics floorId)
)

(factory T3D.NewWorld T3D.IWorld : (construct World))

(method World.RenderGui (IGui gui)-> :
	(Rectf menuRect = 10 10 310 30)
	(gui.FillRect menuRect 0xFF404020)
	
	(WorldOrientation camera)
	(this.GetCameraOrientation camera)
	
	(IStringBuilder sb = (NewPathBuilder))
	(sb.SetFormat 0 0 false false)
	(#build sb "Elevation: " camera.elevation ", Bearing: " camera.bearing)
	
	(gui.DrawText menuRect 0 sb 1 0xFFFFFFFF)
)






