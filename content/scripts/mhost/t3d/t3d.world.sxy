// 3d test

(' #include 
	"!scripts/mhost/mplat.types.sxy"
	"!scripts/mhost/mhost.sxh.sxy"
	"!scripts/mplat.sxh.sxy"
	"!scripts/types.sxy"
	"!scripts/mhost/mhost.include.sxy"
	"!scripts/mhost/widgets/widgets.sxy"
)

(using Sys)
(using Sys.Maths)
(using Sys.Maths.F32)
(using Sys.Type)
(using Sys.Random)
(using Sys.Type.Strings)

(using MPlat)
(using MHost)
(using MHost.OS)
(using MHost.Graphics)
(using Rococo)
(using Rococo.Graphics)
(using Rococo.Audio)
(using Rococo.Entities)

(using Sys.Maths.I32)

(namespace T3D)
(using T3D)

(class World (implements T3D.IWorld)
	(Vec3 cameraPos)
	(WorldOrientation cameraOrientation)
	(IMeshBuilder meshBuilder)
)

(method World.UpdateMouseLook (Vec2 delta)-> :
	(Float32 newBearing = (delta.x + this.cameraOrientation.bearing))
	(Float32 newElevation = (delta.y + this.cameraOrientation.elevation))
	(this.cameraOrientation.bearing = (Sys.Maths.F32.Mod newBearing 360))
	(this.cameraOrientation.elevation = (Sys.Maths.F32.Clamp newElevation -45 45))
)

(method World.GetCameraOrientation (WorldOrientation cameraOrientation)-> :
	(cameraOrientation = this.cameraOrientation)
)

(function MakeBottle (IString meshName) -> :
	(IRodTesselator bottle (RodTesselator))
	(bottle.Clear)
	
	(IInstances instances (Instances))
	
	(MaterialVertexData glass)
	(AnyColour 0 255 0 255 0 255 48 224 -> glass.colour)
	(instances.GetRandomMaterialId (#MaterialCategoryRock) -> glass.id)
	(glass.gloss = 0.2)
	
	(bottle.SetMaterialTop glass)
	(bottle.SetMaterialMiddle glass)
	(bottle.SetMaterialBottom glass)
	
	(Float32 length = (AnyFloat 0.05 0.25))
	(Float32 bottomWidth = (AnyFloat 0.025 0.10))
	(Float32 topWidth = (bottomWidth * (AnyFloat 0.5 1)))
	(Int32 divs = (AnyInt 4 8))
	(bottle.AddTube length bottomWidth topWidth divs)
	
	(Float32 neckLength = (AnyFloat 0.01 0.05))
	(bottle.AddTube neckLength topWidth 0.01 divs)
	
	(MaterialVertexData stopper)
	(instances.GetRandomMaterialId (#MaterialCategoryRock) -> stopper.id)
	(stopper.gloss = 0.2)
	(AnyColour 0 255 0 255 0 255 48 128 -> stopper.colour)
	(bottle.SetMaterialTop stopper)
	(bottle.SetMaterialMiddle stopper)
	(bottle.SetMaterialBottom stopper)
		
	(Float32 stopperLength = (AnyFloat 0.005 0.02))
	(Int32 stopperDivs = (AnyInt 4 6))
	(bottle.AddTube stopperLength (AnyFloat 0.01 0.02) (AnyFloat 0.01 0.035) stopperDivs)
	
	(bottle.CopyToMeshBuilder meshName false false true)
)

(method World.Construct : 
	(this.cameraPos = 0 0 1)
	(this.cameraOrientation = 0 0 0)
	
	(IInstances instances (Instances))
	(instances.LoadMaterialArray "!textures/hv/materials/hi-rez/" 1024) 
	
	(MakeBottle "potion.1") 
	
	(Matrix4x4 model = 
		(1 0 0    0)
		(0 1 0    1)
		(0 0 1 -0.4) 
		(0 0 0    1)
	)
	
	(Vec3 scale = 1 1 1)
	
	(Int64 entityId = (instances.AddBody "potion.1" model scale 0))
	
	(ISceneBuilder scene (SceneBuilder))
	
	(LightSpec light)
	(light.position = 0 0 8)
	(light.direction = 0 0 1)
	(light.fov = 90)
	(light.diffuse = 0.0 0.0 0.0 1.0)
	(light.ambience = 0.1 0.1 0.1 1.0)
	(light.cutoffAngle = 30)
	(light.cutoffPower = 32)
	(light.attenuation = -0.35)
	(light.nearPlane = 0.1)
	(light.farPlane = 1000)
	(light.fogConstant = 1)
	
	(scene.SetLight light 0)
	
	(scene.AddStatics entityId)
)

(factory T3D.NewWorld T3D.IWorld : (construct World))

(method World.RenderGui (IGui gui)-> :
	(Rectf menuRect = 10 10 310 30)
	(gui.FillRect menuRect 0xFF404020)
	
	(WorldOrientation camera)
	(this.GetCameraOrientation camera)
	
	(IStringBuilder sb = (NewPathBuilder))
	(sb.SetFormat 0 0 false false)
	(#build sb "Elevation: " camera.elevation ", Bearing: " camera.bearing)
	
	(gui.DrawText menuRect 0 sb 1 0xFFFFFFFF)
)






