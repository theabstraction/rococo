// 3d test

(using Sys)
(using Sys.Maths)
(using Sys.Maths.F32)
(using Sys.Type)
(using Sys.Random)
(using Sys.Type.Strings)

(using MPlat)
(using MHost)
(using MHost.OS)
(using MHost.Graphics)
(using MHost.Geometry)

(using Rococo)
(using Rococo.Graphics)
(using Rococo.Audio)
(using Rococo.Entities)

(using Sys.Maths.I32)
(using Sys.Geometry.F32)

(namespace T3D)
(using T3D)

(class World (implements T3D.IWorld)
	(Vec3 cameraPos)
	(WorldOrientation cameraOrientation)
	(IMeshBuilder meshBuilder)
)

(method World.UpdateMouseLook (Vec2 delta)-> :
	(Float32 newBearing = (delta.x + this.cameraOrientation.bearing))
	(Float32 newElevation = (this.cameraOrientation.elevation - delta.y))
	(this.cameraOrientation.bearing = (Sys.Maths.F32.Mod newBearing 360))
	(this.cameraOrientation.elevation = (Sys.Maths.F32.Clamp newElevation -45 45))
)

(method World.GetCameraOrientation (WorldOrientation cameraOrientation)-> :
	(cameraOrientation = this.cameraOrientation)
)

(function MakeBottle (IString meshName) -> :
	(IRodTesselator bottle (RodTesselator))
	(bottle.Clear)
	
	(IInstances instances (Instances))
	
	(MaterialVertexData glass)
	(AnyColour 0 255 0 255 0 255 48 224 -> glass.colour)
	(instances.GetRandomMaterialId (#MaterialCategoryRock) -> glass.id)
	(glass.gloss = 0.2)
	
	(bottle.SetMaterialTop glass)
	(bottle.SetMaterialMiddle glass)
	(bottle.SetMaterialBottom glass)
	(bottle.UseSmoothNormals)
	
	(Float32 length = 1.0)
	(Float32 bottomWidth = (AnyFloat 0.15 0.25))
	(Float32 topWidth = (bottomWidth * (AnyFloat 0.5 1)))
	(Int32 divs = 5)
	
	(bottle.AddTube length bottomWidth topWidth divs)
	
	(Float32 neckLength = (AnyFloat 0.04 0.08))
	(bottle.AddTube neckLength topWidth 0.01 divs)
	
	(MaterialVertexData stopper)
	(instances.GetRandomMaterialId (#MaterialCategoryRock) -> stopper.id)
	(stopper.gloss = 0.2)
	(AnyColour 0 255 0 255 0 255 48 128 -> stopper.colour)
	(bottle.SetMaterialTop stopper)
	(bottle.SetMaterialMiddle stopper)
	(bottle.SetMaterialBottom stopper)
		
	(Float32 stopperLength = (AnyFloat 0.005 0.02))
	(Int32 stopperDivs = (AnyInt 4 6))
	(bottle.AddTube stopperLength (AnyFloat 0.01 0.02) (AnyFloat 0.01 0.035) stopperDivs)
	
	(bottle.CopyToMeshBuilder meshName false false true)
)

(function MakeFloor (IString meshName) -> :
	(IRodTesselator rod (RodTesselator))
	(rod.Clear)
	
	(IInstances instances (Instances))
	
	(MaterialVertexData marble)
	(AnyColour 200 255 0 0 0 0 192 192 -> marble.colour)
	(instances.GetRandomMaterialId (#MaterialCategoryMarble) -> marble.id)
	(marble.gloss = 0.1)
	
	(rod.SetMaterialTop marble)
	(rod.SetMaterialMiddle marble)
	(rod.SetMaterialBottom marble)
	
	(rod.SetUVScale 0.05)
	
	(Vec2 b0 = -10  10)
	(Vec2 b1 =  10  10)
	(Vec2 b2 =  10 -10)
	(Vec2 b3 = -10 -10)
	(rod.AddBox 0.25 b0 b1 b2 b3)
	
	(rod.CopyToMeshBuilder meshName false false true)
)

(function MakeLamp (IString meshName) -> :
	(IRodTesselator rod (RodTesselator))
	(rod.Clear)
	
	(IInstances instances (Instances))
	
	(MaterialVertexData marble)
	(AnyColour 200 255 0 0 0 0 192 192 -> marble.colour)
	(instances.GetRandomMaterialId (#MaterialCategoryMarble) -> marble.id)
	(marble.gloss = 0.1)
	
	(rod.SetMaterialTop marble)
	(rod.SetMaterialMiddle marble)
	(rod.SetMaterialBottom marble)
	
	(rod.SetUVScale 0.05)
	
	(Vec2 b0 = -10  10)
	(Vec2 b1 =  10  10)
	(Vec2 b2 =  10 -10)
	(Vec2 b3 = -10 -10)
	(rod.AddTube 0.05 0.025 0.01 8)
	
	(rod.CopyToMeshBuilder meshName false false true)
)

(method World.GetPlayerEyePos (Vec3 pos)-> : (pos = this.cameraPos))
(method World.SetPlayerEyePos (Vec3 pos)-> : (this.cameraPos = pos))

(method World.SetPlayerMoveIntent (Float32 straffeSpeed)(Float32 forwardSpeed)-> :
	/*
	(struct WorldOrientation
		(Degrees bearing) // 0 = North, 90 = East, 180 = South, 270 = West
		(Degrees elevation) // 0 = forward, 90 = up, -90 = down
		(Degrees roll) // anti-clockwise roll about direction vector
	)
	*/


	// Consider the case of three rotations mapping -z to a direction N. The inverse transformation
	// of three rotations is three inverse rotations in reverse order. e.g (Rx.Ry.Rz)' = Rz'.Ry'.Rx'
	
	(if ((forwardSpeed != 0) or (straffeSpeed != 0))
		(Float32 anticlockWiseDegrees = (0 - this.cameraOrientation.bearing))
		
		(Matrix4x4 rZ)
		(Sys.Geometry.F32.RotateAboutZ anticlockWiseDegrees rZ)
		
		(Float32 forwardDelta = (0.1 * forwardSpeed))
		(Float32 straffeDelta = (0.1 * straffeSpeed))
		
		(Vec4 forward = straffeDelta forwardDelta 0 0)
		(Vec4 worldForward)
		
		(Sys.Geometry.F32.MultiplyMatrix4x4fVec4f rZ forward worldForward)
		
		(this.cameraPos.x = (this.cameraPos.x + worldForward.x))
		(this.cameraPos.y = (this.cameraPos.y + worldForward.y))
	)
)

(function AddBottle (ISceneBuilder scene)(IInstances instances)-> :
	(MakeBottle "potion.1") 
	
	(Matrix4x4 model = 
		(1 0 0 0)
		(0 1 0 1.0)
		(0 0 1 -0.5) 
		(0 0 0 1)
	)
	
	(Vec3 noScale = 1 1 1)
	
	(Int64 bottleId = (instances.AddBody "potion.1" model noScale 0))
	(scene.AddStatics bottleId)
)

(function AddFloor (ISceneBuilder scene)(IInstances instances)-> :
	(MakeFloor "floor.1")
	
	(Matrix4x4 model = 
		(1 0 0 0)
		(0 1 0 0)
		(0 0 1 0) 
		(0 0 0 1)
	)
	
	(Vec3 noScale = 1 1 1)
	
	(Int64 floorId = (instances.AddBody "floor.1" model noScale 0))
	(scene.AddStatics floorId)
)

(function AddLight (ISceneBuilder scene)(IInstances instances)-> :
	(MakeLamp "light.cone.1")
	
	(Matrix4x4 rotX)
	(Sys.Geometry.F32.RotateAboutX 45 rotX)
	(Vec4 down = 0 0 -1 0)
	(Vec4 downPrime = rotX * down)
	
	(LightSpec light)
	(light.position = 0 0 2.4)
	(light.direction = downPrime.x downPrime.y downPrime.z)
	(light.fov = 45)
	(light.diffuse = 3.0 3.0 3.0 1.0)
	(light.ambience = 0.6 0.6 0.6 0.5)
	(light.cutoffAngle = 25)
	(light.cutoffPower = 25)
	(light.attenuation = -0.5)
	(light.nearPlane = 0.1)
	(light.farPlane = 1000)
	(light.fogConstant = -0.1)
	
	(Matrix4x4 lightConeTranslate)
	
	(Sys.Geometry.F32.MakeTranslateMatrix light.position lightConeTranslate)
	
	(Matrix4x4 lightConeModel)
	(lightConeModel = lightConeTranslate * rotX)
	
	(Vec3 noScale = 1 1 1)
	
	(Int64 lightCodeId = (instances.AddBody "light.cone.1" lightConeModel noScale 0))
	(scene.AddStatics lightCodeId)
	
	(scene.SetLight light 0)
)

(function AddPoseRun0 (IRigBuilder rb)-> :
	/*              name            parent      length     offset-dx,dy,dz  bone-angles--*/
	(rb.AddBoneX "spine_lower"  ""              0.15       0     0      0       0   0   0)
	(rb.AddBoneX "spine_middle" "spine_lower"   0.15       0     0   0.17       0   1   0)
	(rb.AddBoneX "spine_upper"  "spine_middle"  0.15       0     0   0.17       0   1   0)
	(rb.AddBoneX "femur_l"      "spine_lower"   0.45    -0.1     0 -0.046     150   0   0)
	(rb.AddBoneX "femur_r"      "spine_lower"   0.45     0.1     0 -0.046     230   0   0)
	(rb.AddBoneX "tibia_l"      "femur_l"       0.45       0     0   0.46       0  -5   0)
	(rb.AddBoneX "tibia_r"      "femur_r"       0.45       0     0   0.46     -30  -5   0)
	(rb.AddBoneX "foot_l"       "tibia_l"       0.15       0     0    0.5      100  0   0)
	(rb.AddBoneX "foot_r"       "tibia_r"       0.15       0     0    0.5       80  0   0)
	(rb.AddBoneX "humerus_l"    "spine_upper"   0.25   -0.20     0   0.15      150  0   0)
	(rb.AddBoneX "humerus_r"    "spine_upper"   0.25    0.20     0   0.15      210  0   0)
	(rb.AddBoneX "ulna_l"    	"humerus_l"     0.30       0     0   0.27        0  0   0)
	(rb.AddBoneX "ulna_r"    	"humerus_r"     0.30       0     0   0.27       40  0   0)
	(rb.AddBoneX "hand_l"    	"ulna_l"        0.10       0     0   0.32      -10  0   0)
	(rb.AddBoneX "hand_r"    	"ulna_r"        0.10       0     0   0.32       10  0   0)
	(rb.AddBoneX "skull"  		"spine_upper"   0.25       0     0   0.17       0   0   0)
	
	(rb.CommitToPose "run.0")
)

(function AddPoseRun1 (IRigBuilder rb)-> :
	/*              name            parent      length     offset-dx,dy,dz  bone-angles--*/
	(rb.AddBoneX "spine_lower"  ""              0.15       0     0      0       0   0   0)
	(rb.AddBoneX "spine_middle" "spine_lower"   0.15       0     0   0.17       0  -1   0)
	(rb.AddBoneX "spine_upper"  "spine_middle"  0.15       0     0   0.17       0  -1   0)
	(rb.AddBoneX "femur_l"      "spine_lower"   0.45    -0.1     0 -0.046     230   0   0)
	(rb.AddBoneX "femur_r"      "spine_lower"   0.45     0.1     0 -0.046     150   0   0)
	(rb.AddBoneX "tibia_l"      "femur_l"       0.45       0     0   0.46     -30  -5   0)
	(rb.AddBoneX "tibia_r"      "femur_r"       0.45       0     0   0.46       0  -5   0)
	(rb.AddBoneX "foot_l"       "tibia_l"       0.15       0     0    0.5       80  0   0)
	(rb.AddBoneX "foot_r"       "tibia_r"       0.15       0     0    0.5      100  0   0)
	(rb.AddBoneX "humerus_l"    "spine_upper"   0.25   -0.20     0   0.15      210  0   0)
	(rb.AddBoneX "humerus_r"    "spine_upper"   0.25    0.20     0   0.15      150  0   0)
	(rb.AddBoneX "ulna_l"    	"humerus_l"     0.30       0     0   0.27       40  0   0)
	(rb.AddBoneX "ulna_r"    	"humerus_r"     0.30       0     0   0.27        0  0   0)
	(rb.AddBoneX "hand_l"    	"ulna_l"        0.10       0     0   0.32      -10  0   0)
	(rb.AddBoneX "hand_r"    	"ulna_r"        0.10       0     0   0.32       10  0   0)
	(rb.AddBoneX "skull"  		"spine_upper"   0.25       0     0   0.17       0   0   0)
	
	(rb.CommitToPose "run.1")
)

(function AddSixOClock (IRigBuilder rb)-> :
	/*              name            parent      length     offset-dx,dy,dz  bone-angles--*/
	(rb.AddBoneX "spine_lower"  ""              0.15       0     0      0       0    20  0)
	(rb.AddBoneX "spine_middle" "spine_lower"   0.15       0     0   0.17       0   -10  0)
	(rb.AddBoneX "spine_upper"  "spine_middle"  0.15       0     0   0.17       0   -10  0)
	(rb.AddBoneX "femur_l"      "spine_lower"   0.45    -0.1     0 -0.046     180  -150  0)
	(rb.AddBoneX "femur_r"      "spine_lower"   0.45     0.1     0 -0.046     180   30   0)
	(rb.AddBoneX "tibia_l"      "femur_l"       0.45       0     0   0.46       0    0   0)
	(rb.AddBoneX "tibia_r"      "femur_r"       0.45       0     0   0.46       0    0   0)
	(rb.AddBoneX "foot_l"       "tibia_l"       0.15       0     0    0.5      30    0   0)
	(rb.AddBoneX "foot_r"       "tibia_r"       0.15       0     0    0.5      -10   0   0)
	(rb.AddBoneX "humerus_l"    "spine_upper"   0.25   -0.20     0   0.15      -90 -40   0)
	(rb.AddBoneX "humerus_r"    "spine_upper"   0.25    0.20     0   0.15       80   40   0)
	(rb.AddBoneX "ulna_l"    	"humerus_l"     0.30       0     0   0.27      120   90  0)
	(rb.AddBoneX "ulna_r"    	"humerus_r"     0.30       0     0   0.27        0   0   0)
	(rb.AddBoneX "hand_l"    	"ulna_l"        0.10       0     0   0.32        0   0   0)
	(rb.AddBoneX "hand_r"    	"ulna_r"        0.10       0     0   0.32       40   0   0)
	(rb.AddBoneX "skull"  		"spine_upper"   0.25       0     0   0.17        0   0   0)
	
	(rb.CommitToPose "6-o'clock")
)


(function BuildStraight (IRigBuilder rb)-> :
	/*              name            parent      length     offset-dx,dy,dz  bone-angles--*/
	(rb.AddBoneX "spine_lower"  ""              0.15       0     0      0       0   0   0)
	(rb.AddBoneX "spine_middle" "spine_lower"   0.15       0     0   0.17       0   0   0)
	(rb.AddBoneX "spine_upper"  "spine_middle"  0.15       0     0   0.17       0   0   0)
	(rb.AddBoneX "femur_l"      "spine_lower"   0.45    -0.1     0 -0.046     180   0   0)
	(rb.AddBoneX "femur_r"      "spine_lower"   0.45     0.1     0 -0.046     180   0   0)
	(rb.AddBoneX "tibia_l"      "femur_l"       0.45       0     0   0.46       0   0   0)
	(rb.AddBoneX "tibia_r"      "femur_r"       0.45       0     0   0.46       0   0   0)
	(rb.AddBoneX "foot_l"       "tibia_l"       0.15       0     0    0.5      90   0   0)
	(rb.AddBoneX "foot_r"       "tibia_r"       0.15       0     0    0.5      90   0   0)
	(rb.AddBoneX "humerus_l"    "spine_upper"   0.25   -0.20     0   0.15     180   0   0)
	(rb.AddBoneX "humerus_r"    "spine_upper"   0.25    0.20     0   0.15     180   0   0)
	(rb.AddBoneX "ulna_l"    	"humerus_l"     0.30       0     0   0.27       0   0   0)
	(rb.AddBoneX "ulna_r"    	"humerus_r"     0.30       0     0   0.27       0   0   0)
	(rb.AddBoneX "hand_l"    	"ulna_l"        0.10       0     0   0.32       0   0   0)
	(rb.AddBoneX "hand_r"    	"ulna_r"        0.10       0     0   0.32       0   0   0)
	(rb.AddBoneX "skull"  		"spine_upper"   0.25       0     0   0.17       0   0   0)
)

(function AddGeoff (IRigBuilder rb)-> :
	(BuildStraight rb)	
	(rb.CommitToSkeleton "geoff")
)

(function AddStraightPos (IRigBuilder rb)-> :
	(BuildStraight rb)	
	(rb.CommitToPose "straight")
)

(function AddRigs (ISceneBuilder scene)(IInstances instances)-> :
	(IRigBuilder rb (RigBuilder))
	(rb.ClearBuilder)
	(rb.ClearSkeletons)
	(rb.ClearPoses)
	
	(AddPoseRun0 rb)
	(AddPoseRun1 rb)
	(AddSixOClock rb)
	(AddStraightPos rb)
	(AddGeoff rb)
	
	(Matrix4x4 lowerSpineToWorld =
		(1 0 0   0)
		(0 1 0   1)
		(0 0 1 1.4)
		(0 0 0   1)
	)
		
	(Int64 geoffId = (instances.AddSkeleton "geoff" lowerSpineToWorld))
	(scene.AddDebugObject geoffId)
	(scene.AddDynamicObject geoffId)
	
	(instances.AddAnimationFrame geoffId "straight" 4 false)
	(instances.AddAnimationFrame geoffId "6-o'clock" 4 true)
	(instances.AddAnimationFrame geoffId "straight" 4 false)
	(instances.AddAnimationFrame geoffId "run.0" 4 true)
	(instances.AddAnimationFrame geoffId "6-o'clock" 4 true)
	(instances.AddAnimationFrame geoffId "straight" 4 false)
	(instances.AddAnimationFrame geoffId "run.1" 4 true)
	(instances.AddAnimationFrame geoffId "straight" 4 false)
	(instances.AddAnimationFrame geoffId "run.0" 4 true)
)

(method World.Construct : 
	(this.cameraPos = 0 0 1.8)
	(this.cameraOrientation = 0 0 0)
	
	(IInstances instances (Instances))
	(instances.Clear)
	
	(ISceneBuilder scene (SceneBuilder))
	(scene.Clear)
	
//	(AddBottle scene instances)
	(AddFloor scene instances)
	(AddLight scene instances)
	(AddRigs scene instances)
)

(factory T3D.NewWorld T3D.IWorld : (construct World))

(method World.RenderGui (IGui gui)-> :
	(Rectf menuRect = 10 10 310 30)
	(gui.FillRect menuRect 0xFF404020)
	
	(WorldOrientation camera)
	(this.GetCameraOrientation camera)
	
	(IStringBuilder sb = (NewPathBuilder))
	(sb.SetFormat 0 0 false false)
	(#build sb "Elevation: " camera.elevation ", Bearing: " camera.bearing)
	
	(gui.DrawText menuRect 0 sb 1 0xFFFFFFFF)
)






