// 3d test

(using Sys)
(using Sys.Maths)
(using Sys.Maths.F32)
(using Sys.Type)
(using Sys.Random)
(using Sys.Type.Strings)

(using MPlat)
(using MHost)
(using MHost.OS)
(using MHost.Graphics)
(using MHost.Geometry)

(using Rococo)
(using Rococo.Components.Body)
(using Rococo.Components.Skeleton)
(using Rococo.Components.Animation)
(using Rococo.Graphics)
(using Rococo.Audio)
(using Rococo.Entities)
(using Rococo.ECS)

(using Sys.Maths.I32)
(using Sys.Geometry.F32)

(namespace T3D)
(using T3D)
(using T3D.Objects)

(class World (defines T3D.IWorld)
	(Vec3 cameraPos)
	(WorldOrientation cameraOrientation)
	(IMeshBuilder meshBuilder)
	(IArchive archive)
	(IWorldBuilder wb)
)

(method World.UpdateMouseLook (Vec2 delta)-> :
	(Float32 newBearing = (delta.x + this.cameraOrientation.bearing))
	(Float32 newElevation = (this.cameraOrientation.elevation - delta.y))
	(this.cameraOrientation.bearing = (Sys.Maths.F32.Mod newBearing 360))
	(this.cameraOrientation.elevation = (Sys.Maths.F32.Clamp newElevation -45 45))
	(this.archive.SaveF32 "world.camera.elevation" this.cameraOrientation.elevation)
	(this.archive.SaveF32 "world.camera.bearing" this.cameraOrientation.bearing)
)

(method World.GetCameraOrientation (WorldOrientation cameraOrientation)-> :
	(cameraOrientation = this.cameraOrientation)
)

(method World.GetPlayerEyePos (Vec3 pos)-> : (pos = this.cameraPos))
(method World.SetPlayerEyePos (Vec3 pos)-> : (this.cameraPos = pos))

(method World.SetPlayerMoveIntent (Float32 straffeSpeed)(Float32 forwardSpeed)-> :
	/*
	(struct WorldOrientation
		(Degrees bearing) // 0 = North, 90 = East, 180 = South, 270 = West
		(Degrees elevation) // 0 = forward, 90 = up, -90 = down
		(Degrees roll) // anti-clockwise roll about direction vector
	)
	*/


	// Consider the case of three rotations mapping -z to a direction N. The inverse transformation
	// of three rotations is three inverse rotations in reverse order. e.g (Rx.Ry.Rz)' = Rz'.Ry'.Rx'
	
	(if ((forwardSpeed != 0) or (straffeSpeed != 0))
		(Float32 anticlockWiseDegrees = (0 - this.cameraOrientation.bearing))
		
		(Matrix4x4 rZ)
		(Sys.Geometry.F32.RotateAboutZ anticlockWiseDegrees rZ)
		
		(Float32 forwardDelta = (0.1 * forwardSpeed))
		(Float32 straffeDelta = (0.1 * straffeSpeed))
		
		(Vec4 forward = straffeDelta forwardDelta 0 0)
		(Vec4 worldForward)
		
		(Sys.Geometry.F32.MultiplyMatrix4x4fVec4f rZ forward worldForward)
		
		(this.cameraPos.x = (this.cameraPos.x + worldForward.x))
		(this.cameraPos.y = (this.cameraPos.y + worldForward.y))
		
		(this.archive.SaveVec3 "world.cameraPos" this.cameraPos)
		
		(this.SnapToLandscape this.cameraPos 1.75)
	)
)

(method World.Construct : 
	(this.archive = (GetArchive))
	(this.archive.LoadVec3 "world.cameraPos" this.cameraPos 0 0 127.5)
	
	(Float32 elevation = (this.archive.LoadF32 "world.camera.elevation" 0))
	(Float32 bearing = (this.archive.LoadF32 "world.camera.bearing" 0))
	(this.cameraOrientation = bearing elevation 0)
	
	(IECS ecs (GetECS))
	(ecs.DeprecateAll)
	
	(ISceneBuilder scene (SceneBuilder))
	(scene.Clear)
	
	(AddBottle scene)
	(AddFloor scene)
//	(AddLandscape scene)
	(AddLight scene this)
//	(Rigs.AddRigs scene this)
	
	(this.SnapToLandscape this.cameraPos 1.75)
)

(method World.SnapToLandscape (Vec3 pos)(Float32 altitude)-> :
	(this.wb = (WorldBuilder))
	(TriangleScan scan)
	(Float32 height = (this.wb.GetHeightAt pos.x pos.y scan))
	(if (scan.id != 0)
		(pos.z = (height + altitude))
	)
)

(factory T3D.NewWorld T3D.IWorld : (construct World))

(method World.RenderGui (IGui gui)-> :
	(Rectf menuRect = 10 10 310 30)
	(gui.FillRect menuRect 0xFF404020)
	
	(WorldOrientation camera)
	(this.GetCameraOrientation camera)
	
	(IStringBuilder sb = (NewPathBuilder))
	(sb.SetFormat 0 0 false false)
	(#build sb "Elevation: " camera.elevation ", Bearing: " camera.bearing)
	
	(gui.DrawText menuRect 0 sb 1 0xFFFFFFFF)
)






