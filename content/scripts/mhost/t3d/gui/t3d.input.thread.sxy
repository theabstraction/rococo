(using MHost)
(using MHost.Events)
(using MHost.Graphics)
(using MHost.GuiTypes)
(using MHost.OS)
(using Rococo)
(using Rococo.Graphics)
(using Sys.Maths)
(using Sys.Maths.F32)
(using Sys.Geometry.F32)
(using T3D)

(interface T3D.Gui.IInputThread (attribute essential)
	(extends Sys.ICoroutine)
)

(class GuiInputThread 
	(implements T3D.Gui.IInputThread)
	(IEngine engine)
	(IScreen activeScreen)
	(IEventHandler modalSink)
	(IKeyboard keyboard)
	(IWorld world)
	(T3D.Gui.IOverlayEventHandler overlayHandler)
	(Bool isViewLocked)
)

(method GuiInputThread.Destruct -> :
)

(method GuiInputThread.CaptureMouse (IEventHandler handler)-> :
	(this.modalSink = handler) 
	(this.engine.CaptureMouse)
)

(method GuiInputThread.ReleaseMouse (IEventHandler handler)-> :
	(IEventHandler nullHandler)
	(this.modalSink = nullHandler)
	(this.engine.ReleaseMouse)
)

(method GuiInputThread.Construct (IEngine engine)(IWorld world) : 
	(this.engine = engine)
	(this.world = world)
	(this.isViewLocked = false)
	(this.overlayHandler = (T3D.Gui.NewOverlayButtonHandler world))
)

(factory T3D.Gui.NewGuiInputThread T3D.Gui.IInputThread (IEngine engine)(IWorld world):
	(construct GuiInputThread engine world)
)

(method GuiInputThread.Run -> :
	(IKeyboard keyboard (Keyboard))
	(Int32 leftKey = (keyboard.GetVKeyFromName "A"))
	(Int32 rightKey = (keyboard.GetVKeyFromName "D"))
	(Int32 fireKey = (keyboard.GetVKeyFromName "SPACEBAR"))
	(Int32 upKey = (keyboard.GetVKeyFromName "W"))
	(Int32 downKey = (keyboard.GetVKeyFromName "S")) 
	(Int32 overlayToggleKey = (keyboard.GetVKeyFromName "F1"))
	(Int32 guiToggleKey = (keyboard.GetVKeyFromName "F2"))
	(Int32 lockViewKey = (keyboard.GetVKeyFromName "L"))
	
	(this.engine.SetOverlayToggleKey overlayToggleKey)
	(this.engine.SetGUIToggleKey guiToggleKey)
	
	(IStringBuilder eventString = (Sys.Type.NewParagraphBuilder))
	
	(MouseEvent mouseEvent)
	(KeyboardEvent keyEvent)
	(MHost.GuiTypes.GuiEvent guiEvent)
	
	(IGuiOverlay guiOverlay (MHost.GuiOverlay))
	
	(KeyState keys)
	
	(while this.engine.IsRunning	
		(if (not this.engine.IsAppModal)
			
			(this.engine.PollKeyState keys)
			
			(Float32 straffe = 0)
			(Float32 forward = 0)
		
			(if (IsKeyPressed keys leftKey)
				(straffe -= 1)
			)
			
			(if (IsKeyPressed keys rightKey)
				(straffe += 1)
			)
			
			(if (IsKeyPressed keys upKey)
				(forward += 1)
			)
			
			(if (IsKeyPressed keys downKey)
				(forward -= 1)
			)
			
			(this.world.SetPlayerMoveIntent straffe forward)
		)
		
		(while (this.engine.GetNextKeyboardEvent keyEvent)
			// Required for overlay control
			
			(if ((keyEvent.virtualKeyCode == lockViewKey) and keyEvent.isUp)
				(this.isViewLocked = (not this.isViewLocked))
			)
		)
		
		(while (this.engine.GetNextMouseEvent mouseEvent)
			(if (Sys.Maths.I32.HasFlags mouseEvent.buttons (#EvMouseFlagsRUp))
				(this.isViewLocked = (not this.isViewLocked))
			)
			// Required for overlay control
		)
		
		(while (guiOverlay.GetNextGuiEvent guiEvent)
			(if (guiEvent.eventId == (#EvIdGRisMadeVisible))
				(this.overlayHandler.BuildMenus)
				(continue)
			)
			
			(if (guiEvent.eventId == (#EvIdOverlayButtonClick))
				(eventString.Clear)
				(guiOverlay.AppendEventString eventString guiEvent)
				(T3D.Gui.ButtonArgs buttonArgs = 0)
				(invoke this.overlayHandler eventString buttonArgs)
				(continue)
			)
			
			(if (guiEvent.eventId == (#EvIdLogMessage))
				(eventString.Clear)
				(guiOverlay.AppendEventString eventString guiEvent)
				(this.world.Log eventString)
			)
		)
		
		(Vec2 mouseDelta)
		(this.engine.GetNextMouseDelta mouseDelta)

		(if ((not this.isViewLocked) and (not this.engine.IsAppModal))
			(if ((mouseDelta.x != 0) or (mouseDelta.y != 0))
				(Float32 mouseDegreesPerDelta = T3D.Config.UI.MouseDegreesPerDelta)
				(Vec2 mouseDeltaDegrees = mouseDegreesPerDelta * mouseDelta)
				(this.world.UpdateMouseLook mouseDeltaDegrees)
			)
		)
		
		(yield 5000) // This gives 5ms per poll or 200 Hz
	)
)