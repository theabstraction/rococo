(' #include 
	"!scripts/mhost/mplat.types.sxy"
	"!scripts/mhost/mhost.sxh.sxy"
	"!scripts/mplat.sxh.sxy"
	"!scripts/types.sxy"
	"!scripts/mhost/mhost.include.sxy"
)

(namespace EntryPoint)
	(alias Main EntryPoint.Main)
	
(using Sys.Maths)
(using Sys.Type)
(using Sys.MPlat)
(using MHost)
(using MHost.OS)
(using Rococo)
(using Rococo.Graphics)

(namespace Galaxians)

(struct Sprite
	(IString filename)
	(BitmapLocation location)
)
	
(struct Entity 
	(Vec2i screenPosition)
	(Sprite image1)
	(Sprite image2)
)

(struct Controls
	(Int32 leftKey)
	(Int32 rightKey)
	(Int32 fireKey)
	(Int32 playerDirection)
)

(struct World
	(array Entity aliens)
	(array Entity missiles)
	(Entity player)
	(Entity playerMissile)
	(Int32 direction)
	(Vec2 playerPos)
	(Controls controls)
)

(method World.Construct -> (construct aliens 40) (construct missiles 4) :
	(this.direction = 1)
)

(function InitSprite (IScreenBuilder s)(Sprite bmp)-> :
	(s.GetSpriteSpec bmp.filename bmp.location)
)

(function InitEntitySprites (IScreenBuilder s)(Entity e)-> :
	(InitSprite s e.image1)
	(InitSprite s e.image2)
)

(archetype Galaxians.OnEntity (Entity e)->)

(function ForEachEntity (World world)(OnEntity onEntity)-> :
	(foreach alien # world.aliens (onEntity alien))
	(foreach missile # world.missiles (onEntity missile))
	(onEntity world.player)
	(onEntity world.playerMissile)
)

(function Init (World world)(IEngine engine)-> :
	(BitmapLocation defaultBmpLoc = (0 0 0 0) -1)
	
	(IString pawn1 = "!textures/mhost/pawn.1.tiff")
	(IString pawn2 = "!textures/mhost/pawn.1.tiff")
	
	(#for (Int32 j = 0)(j < 1)(#inc j)
		(Int32 y = (10 + (j * 40)))
		(#for (Int32 i = 0)(i < 4)(#inc i)
			(Int32 x = (10 + (i * 40)))
			(Entity a0 = (x  y) (pawn1 defaultBmpLoc) (pawn2 defaultBmpLoc))
			(world.aliens.Push a0)
		)
	)
	
	(world.player = (70  400) ("!textures/mhost/player.1.tiff" defaultBmpLoc) ("!textures/mhost/player.2.tiff" defaultBmpLoc))
	(world.playerMissile = (-100 -100) ("!textures/mhost/missile.1.tiff" defaultBmpLoc) ("!textures/mhost/missile.2.tiff" defaultBmpLoc))
	
	(IScreenBuilder s = (engine.ScreenBuilder))
	(OnEntity initSprites = (closure (Entity e)-> : (InitEntitySprites s e)))
	(ForEachEntity world initSprites)
	
	(IKeyboard keyboard (Keyboard))
	(world.controls.leftKey = (keyboard.GetVKeyFromName "A"))
	(world.controls.rightKey = (keyboard.GetVKeyFromName "D"))
	(world.controls.fireKey = (keyboard.GetVKeyFromName "SPACEBAR"))
)

(function DrawEntity (Entity e)(IScreenBuilder sb) -> :
	(Int32 alignmentFlags = 0) // centred
	(sb.DrawSprite e.screenPosition alignmentFlags e.image1.location)
)

(function Render (World world)(IEngine engine)(IScreenBuilder s)-> :
	(OnEntity drawSprites = (closure (Entity e)-> : (DrawEntity e s)))
	(ForEachEntity world drawSprites)
	(s.Render)
)

(function TryFireMissile (World world)-> :
	(if (world.playerMissile.screenPosition.y < -30)
		(world.playerMissile.screenPosition = world.player.screenPosition)
	)
)

(function UpdatePlayer (World world)(IEngine engine)-> :
	(KeyState keys)
	(engine.PollKeyState keys)
	
	(world.controls.playerDirection = 0)
	(if (IsKeyPressed keys world.controls.leftKey)
		(world.controls.playerDirection = -1)
	)
	
	(if (IsKeyPressed keys world.controls.rightKey)
		(world.controls.playerDirection = 1)
	)
	
	(if (IsKeyPressed keys world.controls.fireKey)
		(TryFireMissile world)
	)
)

(function TestForAlienCollision (World world)-> :
	(Vec2i tip = world.playerMissile.screenPosition)
	(tip.y = (tip.y - 16))
	
	(foreach alien # world.aliens
		(Vec2i delta = alien.screenPosition - tip)
		(if ((delta.x > -16) and (delta.x < 16))
			(if ((delta.y > 0) and (delta.y < 32))
				(alien.screenPosition.y = -100)
				(world.playerMissile.screenPosition.y = -100)
				(return)
			)
		)
	)
)

(function Advance (World world)-> :
	(foreach alien # world.aliens
		(alien.screenPosition.x = (alien.screenPosition.x + world.direction))
		(if (alien.screenPosition.x > 1000)
			(world.direction = -1)
		)
		(if (alien.screenPosition.x < 10)
			(world.direction = 1)
		)
	)
	
	(if ((world.player.screenPosition.x > 40) and (world.controls.playerDirection < 0))
		(world.player.screenPosition.x = (world.player.screenPosition.x - 2) )
	)
	
	(if ((world.player.screenPosition.x < 1040) and (world.controls.playerDirection > 0))
		(world.player.screenPosition.x = (world.player.screenPosition.x + 2) )
	)
	
	(if (world.playerMissile.screenPosition.y > -32)
		(world.playerMissile.screenPosition.y = (world.playerMissile.screenPosition.y - 8))
		(TestForAlienCollision world)
	)
)

(function Main (Int32 id) -> (Int32 exitCode):
	(ISprites sprites (Sprites))
	(sprites.AddEachSpriteInDirectory "!textures/mhost")
	(sprites.LoadAllSprites)
	
	(IEngine engine (Engine))
	(IScreenBuilder sb = (engine.ScreenBuilder))
	
	(World world ())
	(Init world engine)
	
	(while engine.IsRunning
		(UpdatePlayer world engine)
		(Advance world)
		(Render world engine sb)
		(engine.YieldForSystemMessages 5)
	)
)