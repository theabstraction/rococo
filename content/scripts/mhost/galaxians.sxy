(' #import (mhost "MHost"))

(namespace EntryPoint)
	(alias Main EntryPoint.Main)
	
(using Sys)
(using Sys.Maths)
(using Sys.Maths.I32)
(using Sys.Type)
(using MPlat)

(using MHost)
(using MHost.OS)
(using Rococo)
(using Rococo.Graphics)
(using Rococo.Audio)

(namespace Galaxians)
(using Galaxians)

(struct Sprite
	(IString filename)
	(BitmapLocation location)
)
	
(struct Entity 
	(Vec2i screenPosition)
	(Sprite image)
)

(interface MHost.IWorld
	(ForEachEntityInWorld (OnEntity onEntity)->)
	(Init ->)
	(Advance ->)
	(RenderGui (IGui gui)-> )
)

(interface Galaxians.IUIThread (extends Sys.ICoroutine)
	(GetIntent -> (Int32 direction)(Bool fire))
)

(class UIThread (implements Galaxians.IUIThread)
	(IEngine engine)
	(Int32 direction)
	(Bool fire)
)

(method UIThread.Construct (IEngine engine):
	(this.engine = engine)
	(this.direction = 0)
	(this.fire = false)
)

(factory Galaxians.NewUIThread Sys.ICoroutine (IEngine engine):
	(construct UIThread engine)
	(Int32 direction)
	(Bool fire)
)

(method UIThread.Run -> :
	(IKeyboard keyboard (Keyboard))
	
	(Int32 leftKey = (keyboard.GetVKeyFromName "A"))
	(Int32 rightKey = (keyboard.GetVKeyFromName "D"))
	(Int32 fireKey = (keyboard.GetVKeyFromName "SPACEBAR"))
	
	(KeyboardEvent keyEvent)
	(MouseEvent mouseEvent)
	
	(KeyState keys)		
	(while this.engine.IsRunning
		(while (this.engine.GetNextMouseEvent mouseEvent)
		)
			
		(while (this.engine.GetNextKeyboardEvent keyEvent)
		)
		
		(this.engine.PollKeyState keys)
		
		(this.direction = 0)
		
		(if (IsKeyPressed keys leftKey)
			(this.direction -= 1)
		)
		
		(if (IsKeyPressed keys rightKey)
			(this.direction += 1)
		)
		
		(this.fire = 0)
		
		(if (IsKeyPressed keys fireKey)
			(this.fire = 1)
		)
		
		(yield 1000)
	)
)

(method UIThread.GetIntent -> (Int32 direction)(Bool fire):
	(direction = this.direction)
	(fire = this.fire)
)

(class Galaxians (implements MHost.IWorld)
	(IEngine engine)
	(ILegacySoundControl sound)
	(IUIThread uiThread)
	(array Entity aliens)
	(array Entity missiles)
	(Entity player)
	(Entity playerMissile)
	(Int32 direction)
	(Vec2 playerPos)
	(Int32 killCount)
	(Int64 deathAt)
	(Int32 lives)
	(Entity playerExplosion)
	(Entity enemyExplosion)
	(Int64 enemyHitAt)
	(IStringBuilder sbScore)
	(Int32 playerDirection)
)

(method Galaxians.Construct (IEngine engine) (ILegacySoundControl sound) -> (construct aliens 54) (construct missiles 4) :
	(this.direction = 1)
	(this.engine = engine)
	(this.sound = sound)
	(this.uiThread = (Galaxians.NewUIThread engine))
	(this.sbScore = NewPathBuilder)
	(ICoroutineControl coroutines (Coroutines))
	(coroutines.Add this.uiThread)
)

(factory MHost.Galaxians MHost.IWorld (IEngine engine) (ILegacySoundControl sound) : (construct Galaxians engine sound))

(function InitSprite (IEngine engine)(Sprite bmp)-> :
	(engine.GetSpriteSpec bmp.filename bmp.location)
)

(archetype Galaxians.OnEntity (Entity e)->)

(method Galaxians.ForEachEntityInWorld (OnEntity onEntity)-> :
	(foreach alien # this.aliens (onEntity alien))
	(foreach missile # this.missiles (onEntity missile))
	(onEntity this.player)
	(onEntity this.playerMissile)
)

(method Galaxians.Init -> :
	(BitmapLocation defaultBmpLoc = (0 0 0 0) -1 (0 0))
	
	(IString pawn1 = "!textures/mhost/galaxians/pawn.1.tiff")
	(IString pawn2 = "!textures/mhost/galaxians/pawn.2.tiff")
	(IString pawn3 = "!textures/mhost/galaxians/pawn.3.tiff")
	(IString pawn4 = "!textures/mhost/galaxians/pawn.4.tiff")
	
	(this.killCount = 0)
	(this.enemyHitAt = 0)
	
	(this.playerExplosion =  (0  -100) ("!textures/mhost/galaxians/explosion.tiff" defaultBmpLoc))
	(this.enemyExplosion = (0  -100) ("!textures/mhost/galaxians/explosion.tiff" defaultBmpLoc))
	
	// 4 enemy missiles
	(
		(#for (Int32 i = 0)(i < 4)(i += 1)
			(Entity a0 = (1  -40) ("!textures/mhost/galaxians/missile.2.tiff" defaultBmpLoc))
			(this.missiles.Push a0)
		)
	)
	
	// 2 yellow meanies
	(
		(Int32 y = 80)
		(Int32 x0 = 130)
		(Entity a0 = (x0  y) (pawn4 defaultBmpLoc))
		(this.aliens.Push a0)
		(Int32 x1 = 250)
		(Entity a1 = (x1 y) (pawn4 defaultBmpLoc))
		(this.aliens.Push a1)
	)
	
	// 1 line of 6 red meanies
	(
		(Int32 y = 120)
		(#for (Int32 i = 0)(i < 6)(i += 1)
			(Int32 x = (90 + (i * 40)))
			(Entity a0 = (x  y) (pawn3 defaultBmpLoc))
			(this.aliens.Push a0)
		)
	)
	
	// 1 line 8 purple meanies
	(
		(Int32 y = 160)
		(#for (Int32 i = 0)(i < 8)(i += 1)
			(Int32 x = (50 + (i * 40)))
			(Entity a0 = (x  y) (pawn2 defaultBmpLoc))
			(this.aliens.Push a0)
		)
	)
	
	
	// 3 lines of 10 blue meanies
	(#for (Int32 j = 3)(j < 6)(j += 1)
		(Int32 y = (80 + (j * 40)))
		(#for (Int32 i = 0)(i < 10)(i += 1)
			(Int32 x = (10 + (i * 40)))
			(Entity a0 = (x  y) (pawn1 defaultBmpLoc))
			(this.aliens.Push a0)
		)
	)
	
	(this.player = (500 8000) ("!textures/mhost/galaxians/player.1.tiff" defaultBmpLoc))
	(this.playerMissile = (-100 -100) ("!textures/mhost/galaxians/missile.1.tiff" defaultBmpLoc))
	(this.deathAt = Sys.OS.ClockTicks)
	(this.lives = 3)
	
	(OnEntity initSprites = 
		(closure (Entity e)-> :
			(InitSprite this.engine e.image)
		)
	)
	
	(this.ForEachEntityInWorld initSprites)
	
	
	(InitSprite this.engine this.playerExplosion.image)
	(InitSprite this.engine this.enemyExplosion.image)
	
	(this.sound.SetWave 0 (#ELegacySoundShapeSine) 1000 1.0)
	(this.sound.SetWave 1 (#ELegacySoundShapeSine) 320 1.0)
	(this.sound.SetWave 2 (#ELegacySoundShapeSine) 360 1.0)
	(this.sound.SetWave 3 (#ELegacySoundShapeSine) 400 1.0)
	(this.sound.SetWave 4 (#ELegacySoundShapeSine) 440 1.0)
	(this.sound.SetWave 5 (#ELegacySoundShapeSine) 480 1.0)
	(this.sound.SetWave 6 (#ELegacySoundShapeSine) 540 1.0)
	(this.sound.SetWave 7 (#ELegacySoundShapeSine) 600 1.0)
	(this.sound.SetWave 8 (#ELegacySoundShapeSine) 640 1.0)
	(this.sound.SetWave 9 (#ELegacySoundShapeSine) 712 1.0)
	
	(this.sound.SetMasterVolumne 0.8)
	
	(#for (Int32 i = 0)(i < 10)(i += 1)
		(this.sound.SetChannelVolume i 1.0 1.0)
		(this.sound.SetEnvelope i 2.0 0.1 0.5 1.0)
	)
)

(function RenderLives (Entity player)(IGui gui)(Int32 count)-> :
	(#for (Int32 i = 0)(i < count)(i += 1)
		(Vec2i pos)
		(pos.x =  (100 + (i * 64)))
		(pos.y = 640)
		(DrawSprite gui pos player.image)
	)
)

(function ExpandRect (Recti rect)(Int32 span)(Vec2i centre)-> :
	(Int32 negspan = (0 - span))
	(rect = negspan negspan span span)
	(rect.left += centre.x)
	(rect.right += centre.x)
	(rect.bottom += centre.y)
	(rect.top += centre.y)
)

(function ToVec2f (Vec2i a)(Vec2 b) -> :
	(ToFloat32 a.x -> b.x)
	(ToFloat32 a.y -> b.y)
)

(function ToRectf (Recti a)(Rectf b) -> :
	(ToFloat32 a.left -> b.left)
	(ToFloat32 a.top -> b.top)
	(ToFloat32 a.right -> b.right)
	(ToFloat32 a.bottom -> b.bottom)
)

(function DrawStretchedSprite(IGui gc)(Recti rect)(Entity e) -> :
	(Rectf rectF)
	(ToRectf rect rectF)
	(gc.StretchSprite rectF e.image.location)
)

(function DrawSprite(IGui gc)(Vec2i pos)(Sprite s)-> :
	(Vec2 fpos)
	(ToVec2f pos /* -> */ fpos)
	(gc.DrawSprite fpos 0 s.location)
)

(function DrawEntity(IGui gc)(Entity e)-> :
	(DrawSprite gc e.screenPosition e.image)
)

(method Galaxians.RenderGui (IGui gc) -> :
	(OnEntity drawSprites = (closure (Entity e)-> : (DrawEntity gc e)))
	
	(this.ForEachEntityInWorld drawSprites)
	
	(if (this.deathAt != 0)
		(Int64 peakBoom = Sys.OS.ClockHz)
		(Int64 delta = ((Sys.OS.ClockTicks) - this.deathAt))
		(if (delta < peakBoom)
			(DrawSprite gc this.playerExplosion.screenPosition this.player.image)
			(Int64 boomTick = ( peakBoom / 64 ))
			(Int32 span = (delta / boomTick))
			
			(Recti rect)
			(ExpandRect rect span this.playerExplosion.screenPosition)
			
			(DrawStretchedSprite gc rect this.playerExplosion)
		)
	)
	
	(if (this.enemyHitAt != 0)
		(Int64 peakBoom = Sys.OS.ClockHz)
		(Int64 delta = ((Sys.OS.ClockTicks) - this.enemyHitAt))
		(if (delta < peakBoom)
			(Int64 boomTick = ( peakBoom / 32 ))
			(Int32 span = (32 - (delta / boomTick)))
			(Recti rect)
			(ExpandRect rect span this.enemyExplosion.screenPosition)
			
			(DrawStretchedSprite gc rect this.enemyExplosion)
		else
			(this.enemyHitAt = 0)
		)
	)
	
	(if (this.lives > 0)
		(if (this.deathAt == 0)
			(RenderLives this.player gc (this.lives - 1))
		else
			(RenderLives this.player gc this.lives) // I can hear you
		)
	)
	
	(this.sbScore.Clear)
	(#build this.sbScore "Score: " this.killCount)
	
	(Vec2 screenSpan)
	(gc.GetScreenSpan screenSpan)
	(Int32 alignmentFlags = 0)
	(Rectf rect = 0 10 screenSpan.x 30)
	(gc.DrawText rect alignmentFlags this.sbScore 1 0xFFFFFF80)
)

(function TryFireMissile (Entity playerMissile)(Entity player)-> :
	(if (playerMissile.screenPosition.y < -30)
		(playerMissile.screenPosition = player.screenPosition)
	)
)

(function UpdatePlayer (Galaxians g)-> :
	(Bool fire)
	(g.uiThread.GetIntent -> g.playerDirection fire)
	
	(if fire
		(TryFireMissile g.playerMissile g.player)
	)
)

(function TestForAlienCollision (Galaxians g)-> :
	(Vec2i tip = g.playerMissile.screenPosition)
	(tip.y -= 8)
	
	(foreach alien # g.aliens
		(Vec2i delta = alien.screenPosition - tip)
		(if ((delta.x > -12) and (delta.x < 12))
			(if ((delta.y > 0) and (delta.y < 16))
				(g.enemyExplosion.screenPosition = alien.screenPosition)
				(alien.screenPosition.y = -100)
				(g.playerMissile.screenPosition.y = -100)
				(g.killCount += 1)
				(Sys.OS.ClockTicks -> g.enemyHitAt)
				(return)
			)
		)
	)
)

(function GetEnemyLaunchPosition (Galaxians g) (Vec2i pos)-> :
	(Int32 smallestDxi = 1000)
	(foreach alien # g.aliens
		(Int32 dx = (g.player.screenPosition.x - alien.screenPosition.x))
		(Int32 dxi = (Sys.Maths.I32.Abs dx))
		(if (((dxi < smallestDxi) and (g.player.screenPosition.y > 0)) and ((Sys.Random.AnyFloat 0 1) > 0.5))
			(smallestDxi = dxi)
			(pos = alien.screenPosition)
		)
	)
)

(function TryFireEnemyMissile (Galaxians g)-> :
	(if (g.player.screenPosition.y < 8000)
		(foreach missile # g.missiles
			(if (missile.screenPosition.y < 32)
				(GetEnemyLaunchPosition g missile.screenPosition)
				(return)
			)
		)
	)
)

(function OnPlayerKilled (Galaxians g)-> :
	(g.playerExplosion.screenPosition = g.player.screenPosition)
	(g.player.screenPosition.y = -10000)
	(Sys.OS.ClockTicks -> g.deathAt) 
	(g.lives -= 1)
)

(function TryHitPlayer (Galaxians g)(Entity missile) -> :
	(Vec2i delta = g.player.screenPosition - missile.screenPosition)
	(Int32 dx = (Sys.Maths.I32.Abs delta.x))
	(Int32 dy = (Sys.Maths.I32.Abs delta.y))
	
	(if (g.player.screenPosition.y < 8000)
		(if ((dx < 16) and (dy < 16))
			(OnPlayerKilled g)
		)
	)
)

(method Galaxians.Advance  -> :
	(UpdatePlayer this)

	(foreach alien # this.aliens
		(if (alien.screenPosition.y > 0)
			(if (alien.screenPosition.x > 800)
				(this.direction = -1)
			)
			(if (alien.screenPosition.x < 200)
				(this.direction = 1)
			)
		)
	)
	
	(Int32 straffeSpeed = 1)
	
	(if (this.killCount > 40)
		(straffeSpeed = 5)
	else 
		(if (this.killCount > 30)
			(straffeSpeed = 4)
		else
			(if (this.killCount > 20)
				(straffeSpeed = 3)
			else
				(if (this.killCount > 10)
					(straffeSpeed = 2)
				)
			)
		)
	)
	
	(Int32 delta = (this.direction * straffeSpeed))
	
	(foreach a # this.aliens
		(a.screenPosition.x += delta)
	)
	
	(if ((this.player.screenPosition.x > 200) and (this.playerDirection < 0))
		(this.player.screenPosition.x -= 2)
	)
	
	(if ((this.player.screenPosition.x < 800) and (this.playerDirection > 0))
		(this.player.screenPosition.x += 2)
	)
	
	(if (this.playerMissile.screenPosition.y > -32)
		(this.playerMissile.screenPosition.y -= 8)
		(TestForAlienCollision this)
	)
	
	(if ((Sys.Random.AnyFloat 0 1) > 0.9)
		(TryFireEnemyMissile this)
	)
	
	(foreach missile # this.missiles
		(if (missile.screenPosition.y > this.player.screenPosition.y)
			(missile.screenPosition.y = -40)
		)
		
		(if (missile.screenPosition.y >= 0)
			(missile.screenPosition.y += 6)
			
			(TryHitPlayer this missile)
		)
	)
	
	(if (this.deathAt != 0)
		(Int64 now = Sys.OS.ClockTicks)
		(Int64 delta = (now - this.deathAt))
		(Int64 spawnTime = (4 * Sys.OS.ClockHz))
		(if (delta > spawnTime)
			(this.deathAt = 0)
			(this.player.screenPosition = 500 610)
		)
	)
)

(function Main (Int32 id) -> (Int32 exitCode):
	(ISprites sprites (Sprites))
	(sprites.AddEachSpriteInDirectory "!textures/mhost/galaxians")
	(sprites.LoadAllSprites)
	
	(IEngine engine (Engine))
	(ILegacySoundControl sound (Rococo.Audio.LegacySoundControl))
	
	(IWorld world (Galaxians engine sound))
	(world.Init)
	
	(ICoroutineControl coroutines (Coroutines))
	
	(while engine.IsRunning
		(world.Advance)
		
		(GuiPopulator p =
			(closure (IGui gui) -> : 
				(world.RenderGui gui)
			)
		)
		
		(engine.Render p)
		
		(engine.YieldForSystemMessages 5)
		
		(while (coroutines.Continue > 0))
	)
	
	(coroutines.ReleaseAll)
)