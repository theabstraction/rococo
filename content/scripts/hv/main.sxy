(' #file.type rococo.hv)

(' #include 
	"!scripts/hv.sxh.sxy"
	"!scripts/types.sxy"
)

(namespace EntryPoint)
	(alias Main EntryPoint.Main)
	
(using Sys.Maths)
(using HV.Graphics)
(using HV.Entities)
(using HV)

(function DefinePoint -> :
	(IMeshBuilder mb (MeshBuilder))
	(mb.Clear)
	(mb.Begin "mesh.platonic.point")
	(Vertex bottomLeft  = (-0.001  -0.001 0) (0 0 1) 0x00000000 0x00000000 (1 1))
	(Vertex topLeft     = (-0.001   0.001 0) (0 0 1) 0x00000000 0x00000000 (1 0))
	(Vertex bottomRight = ( 0.001  -0.001 0) (0 0 1) 0x00000000 0x00000000 (0 1))
	(mb.AddTriangle  topLeft bottomLeft   bottomRight)
	(mb.End)
)

(function DefinePlatonicSquare -> :
	(IMeshBuilder mb (MeshBuilder))
	(mb.Clear)
	(mb.Begin "mesh.platonic.square")
	
	/*
	(struct Vertex
		(Sys.Maths.Vec3 position)
		(Sys.Maths.Vec3 normal)
		(Int32 emissiveColour)
		(Int32 diffuseColour)
		(Sys.Maths.Vec2 uv)
	)
	*/
	
	// Top face
	(
		(Vertex bottomLeft  = (-0.5 -0.5 -0.5) (0 0 1) 0x808080FF 0xFF0000FF (1 1))
		(Vertex topLeft     = (-0.5  0.5 -0.5) (0 0 1) 0x808080FF 0xFF0000FF (1 0))
		(Vertex bottomRight = ( 0.5 -0.5  0.5) (0 0 1) 0x808080FF 0xFF0000FF (0 1))
		(Vertex topRight    = ( 0.5  0.5  0.5) (0 0 1) 0x808080FF 0xFF0000FF (0 0))
		
		(mb.AddTriangle  topLeft bottomLeft   topRight)
		(mb.AddTriangle bottomRight topRight   bottomLeft)
	)
	
	// Nearer face
	(
		(Vertex bottomLeft  = (-1.0 -1.0 0) (0 0 1) 0x808080FF 0xFF00008F (1 1))
		(Vertex topLeft     = (-1.0  1.0 0) (0 0 1) 0x808080FF 0xFF00008F (1 0))
		(Vertex bottomRight = ( 1.0 -1.0 0) (0 0 1) 0x808080FF 0xFF00008F (0 1))
		(Vertex topRight    = ( 1.0  1.0 0) (0 0 1) 0x808080FF 0xFF00008F (0 0))
		(mb.AddTriangle bottomLeft topLeft  topRight)
		(mb.AddTriangle bottomRight topRight   bottomLeft)
	)
	
	(mb.End)
)

(function Main (Int32 id)->(Int32 exitCode):
	(IMeshBuilder mb (MeshBuilder))
	(IInstances instances (Instances))
	(ISceneBuilder scene (SceneBuilder))
	(ICamera camera (Camera))
	
	(DefinePoint)
	(DefinePlatonicSquare)
	
	(instances.Begin "square.1")
	(instances.SetMeshByName "mesh.platonic.square" "!portraits/charlotte.jpg")
	(Int64 squareId = instances.End)
	
	(Vec3 sqPos = 0 5 0)
	(instances.SetPosition squareId sqPos)
	
	(instances.Begin "player.1")
	(instances.SetMeshByName "mesh.platonic.point" "!portraits/charlotte.jpg")
	(Int64 playerId = instances.End)
	
	(IPlayer player (Player 0))
	(player.SetPlayerEntity playerId)
	(player.SetControlFPS)
	
	(Vec3 pos = 3 3 0)
	(instances.SetPosition playerId pos)
	
	(scene.AddStatics squareId)
	
	(camera.SetRHProjection 60 0.3 1000)
	(camera.FollowEntity playerId)
	(camera.OrientateWithEntity playerId 0)
	(scene.SetClearColour 0.25 0.25 0)
)