(' #file.type rococo.hv)

(' #include 
	"!scripts/hv.sxh.sxy"
	"!scripts/types.sxy"
)

(namespace EntryPoint)
	(alias Main EntryPoint.Main)
	
(using Sys.Maths)
(using HV.Graphics)

(function DefinePlatonicSquare -> :
	(IMeshBuilder mb (MeshBuilder))
	(mb.Clear)
	(mb.Begin "mesh.platonic.square")
	
	/*
	(struct Vertex
		(Sys.Maths.Vec3 position)
		(Sys.Maths.Vec3 normal)
		(Int32 emissiveColour)
		(Int32 diffuseColour)
		(Sys.Maths.Vec2 uv)
	)
	*/
	
	// Top face
	(
		(Vertex bottomLeft  = (-0.5 -0.5 0.5) (0 0 1) 0x808080FF 0xFF0000FF (0 0))
		(Vertex topLeft     = (-0.5  0.5 0.5) (0 0 1) 0x808080FF 0xFF0000FF (0 1))
		(Vertex bottomRight = ( 0.5 -0.5 2.5) (0 0 1) 0x808080FF 0xFF0000FF (1 0))
		(Vertex topRight    = ( 0.5  0.5 2.5) (0 0 1) 0x808080FF 0xFF0000FF (1 1))
		
		(mb.AddTriangle topLeft bottomLeft   topRight)
		(mb.AddTriangle bottomRight topRight bottomLeft)
	)
	
	// Nearer face
	(
		(Vertex bottomLeft  = (-1.0 -1.0 1.5) (0 0 1) 0x808080FF 0xFF00008F (0 0))
		(Vertex topLeft     = (-1.0  1.0 1.5) (0 0 1) 0x808080FF 0xFF00008F (0 1))
		(Vertex bottomRight = ( 1.0 -1.0 1.5) (0 0 1) 0x808080FF 0xFF00008F (1 0))
		(Vertex topRight    = ( 1.0  1.0 1.5) (0 0 1) 0x808080FF 0xFF00008F (1 1))
		(mb.AddTriangle topLeft bottomLeft topRight)
		(mb.AddTriangle bottomRight topRight bottomLeft)
	)
	
	(mb.End)
)

(function GetProjectionMatrix (Matrix4x4 output)-> :

	(ICamera camera (Camera))
	(Float32 aspect = camera.AspectRatio)

	(Float32 radiansPerDegree = ((#pi) / 180))
	(Float32 fov = 45.0)
	(Float32 tanFov = (F32.Tan ((radiansPerDegree * fov) / 2)))
    (Float32 yScale = (1 / tanFov))
	(Float32 xScale = (yScale * aspect))
	
	(Float32 far = 1000)
	(Float32 near = 0.2)
	
	(Float32 df = (near - far))
	(Float32 z0 = ((far + near) / df ))
	(Float32 zScale = (0 + z0))
	
	(Float32 w0 = ((far * near) * 2))
	(Float32 wScale = (w0 / df ))
	
	(Matrix4x4 proj = 
		(xScale		0       0   	 0)
		(0	   yScale   	0        0)
		(0			0  zScale   wScale)
		(0			0      -1        1)
	)
	
	(output = proj)
)

(function Main (Int32 id)->(Int32 exitCode):
	(IMeshBuilder mb (MeshBuilder))
	(IInstances instances (Instances))
	(ISceneBuilder scene (SceneBuilder))
	(ICamera camera (Camera))
	
	(DefinePlatonicSquare)
	
	(instances.Begin "square.1")
	(instances.SetMeshByName "mesh.platonic.square" "!portraits/charlotte.jpg")
	(Int64 squareId = instances.End)
	
	(scene.AddStatics squareId)
	
	(Matrix4x4 proj)
	(GetProjectionMatrix proj)
	(camera.SetProjection proj)
//	(Quat q = (0 1 0) 0)
	(Vec3 pos = 0 0 3)
//	(camera.SetOrientation q)
	(camera.SetPosition pos)
	(scene.SetClearColour 0.25 0.25 0)
)