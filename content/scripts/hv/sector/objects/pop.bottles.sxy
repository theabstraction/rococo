(' #file.type rococo.hv)

(' #include 
	"!scripts/mplat.sxh.sxy"
	"!scripts/hv.sxh.sxy"
	"!scripts/hv/hv.types.sxy"
	"!scripts/types.sxy"
	"!scripts/mplat.types.sxy"
)

(namespace EntryPoint)
	(alias Main EntryPoint.Main)
	

(using HV)
(using Rococo)
(using Rococo.Graphics)
(using Rococo.Entities)
(using Sys.Type)
(using Sys.Maths)
(using Sys.Geometry.F32)
(using Sys.Random)

(function AddItemToSector (ISectorLayout sector)(IString meshName)-> :
	(sector.AddMiscScenery meshName "scenery.misc")
)

(function MakeBottle (IString meshName) -> :
	(IRodTesselator bottle (RodTesselator))
	(bottle.Clear)
	
	(IInstances instances (Instances))
	
	(MaterialVertexData glass)
	(AnyColour 0 255 0 255 0 255 48 224 -> glass.colour)
	(instances.GetRandomMaterialId (#MaterialCategoryRock) -> glass.id)
	(glass.gloss = 0.2)
	
	(bottle.SetMaterialTop glass)
	(bottle.SetMaterialMiddle glass)
	(bottle.SetMaterialBottom glass)
	
	(Float32 length = (AnyFloat 0.05 0.25))
	(Float32 bottomWidth = (AnyFloat 0.025 0.10))
	(Float32 topWidth = (bottomWidth * (AnyFloat 0.5 1)))
	(Int32 divs = (AnyInt 4 8))
	(bottle.AddTube length bottomWidth topWidth divs)
	
	(Float32 neckLength = (AnyFloat 0.01 0.05))
	(bottle.AddTube neckLength topWidth 0.01 divs)
	
	(MaterialVertexData stopper)
	(instances.GetRandomMaterialId (#MaterialCategoryRock) -> stopper.id)
	(stopper.gloss = 0.2)
	(AnyColour 0 255 0 255 0 255 48 128 -> stopper.colour)
	(bottle.SetMaterialTop stopper)
	(bottle.SetMaterialMiddle stopper)
	(bottle.SetMaterialBottom stopper)
		
	(Float32 stopperLength = (AnyFloat 0.005 0.02))
	(Int32 stopperDivs = (AnyInt 4 6))
	(bottle.AddTube stopperLength (AnyFloat 0.01 0.02) (AnyFloat 0.01 0.035) stopperDivs)
	
	(bottle.CopyToMeshBuilder meshName false false true)
)

(function Main (Int32 id)->(Int32 exitCode):
	(ISectorEnumerator sectors (SectorEnumerator))
	(Int32 nSectors = sectors.Count)
			
	(Int32 addItemFlags = 0)
	(addItemFlags = (addItemFlags + (#AddItemFlagsRandomHeading)))
	(addItemFlags = (addItemFlags + (#AddItemFlagsRandomPosition)))
	(addItemFlags = (addItemFlags + (#AddItemFlagsAlignEdge)))
		
	(IStringBuilder sb = NewPathBuilder)
	
	(#for (Int32 j = 0)(j < 5)(#inc j)
		(sb.Clear)
		(#build sb "object.bottle." j)
		(MakeBottle sb)
	)
	
	(IInstances instances (Instances))
	
	(#for (Int32 i = 0)(i < nSectors)(#inc i)
		(ISectorLayout sector = (sectors.GetSector i))
		(sector.DeleteItemsWithMesh "object.bottle.")
			
		(#for (Int32 j = 1)(j < 50)(#inc j)
			(ObjectCreationSpec ocs)
			(sb.Clear)
			(#build sb "object.bottle." (AnyInt 0 4))
			
			(Matrix4x4 model = 
				(1 0 0 0)
				(0 1 0 0)
				(0 0 1 0)
				(0 0 0 1)
			)
			
			(Vec3 scale = 1 1 1)
			(Int64 bottleId = (instances.AddBody sb model scale 0))
			(if (not (sector.PlaceItemOnUpFacingQuad bottleId))
				(instances.Delete bottleId)
			)
		)
	)
)