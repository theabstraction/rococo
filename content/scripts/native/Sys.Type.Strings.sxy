(using Sys.Type)

(interface Sys.Type.IString
	(Length -> (Int32 length))
	(Buffer -> (Pointer buffer))
)

(interface Sys.Type.IBuffer
	(Capacity -> (Int32 capacity))
	(Data -> (Pointer data))
)

(class NativeException
	(implements IException)
	(Int32 errorCode)
	(Pointer msgHandle)
)

(method NativeException.ErrorCode -> (Int32 errorCode):
	(errorCode = this.errorCode)
)

(method NativeException.Message -> (IString message):
	(Sys.Type.GetSysMessage this.msgHandle -> message)
)

(class StringConstant
	(implements IString)
	(Int32 length)
	(Pointer buffer)
	(Pointer sExpression)
)

(method StringConstant.Length -> (Int32 charCount):
    (charCount = this.length)
)

(method StringConstant.Buffer -> (Pointer buffer):
    (buffer = this.buffer)
)
	
(class MemoImpl
    (implements IString)
    (Int32 length)
    (Pointer buffer)
)

(method MemoImpl.Length -> (Int32 charCount):
	(charCount = this.length)
)

(method MemoImpl.Buffer -> (Pointer buffer):
    (buffer = this.buffer)
)

(method MemoImpl.Destruct -> :
    (Sys.Strings.Native.FreeMemoString (this.buffer))
)

(method MemoImpl.Construct (IString s) -> :
	(Sys.Strings.Native.CreateMemoString s -> this.buffer this.length)	
)

(interface Sys.Type.Formatters.AsDecimal (Null -> ))
(interface Sys.Type.Formatters.AsHex (Null -> ))
(interface Sys.Type.Formatters.AsSpecE (Null -> ))
(interface Sys.Type.Formatters.AsSpecF (Null -> ))
(interface Sys.Type.Formatters.AsSpecG (Null -> ))
(interface Sys.Type.Formatters.AsNewLine (Null -> ))
(interface Sys.Type.Formatters.AsUnsigned (Null -> ))
(interface Sys.Type.Formatters.AsFormat (Null -> ))

(function FormatDecimal -> (Sys.Type.Formatters.AsDecimal nullType):)
(function FormatHex -> (Sys.Type.Formatters.AsHex nullType):)
(function FormatUnsigned -> (Sys.Type.Formatters.AsUnsigned nullType):)
(function FormatSpecE -> (Sys.Type.Formatters.AsSpecE nullType):)
(function FormatSpecF -> (Sys.Type.Formatters.AsSpecF nullType):)
(function FormatSpecG -> (Sys.Type.Formatters.AsSpecG nullType):)
(function FormatNewLine -> (Sys.Type.Formatters.AsNewLine nullType):)
(function FormatFormat -> (Sys.Type.Formatters.AsFormat nullType):)

(using Sys.Type.Formatters)

// N.B - C++ routines in the Rococo codebase will assume the underlying implementation of an IStringBuilder is a FastStringBuilder and thus be able to access critical data directly
(class FastStringBuilder 
	(implements IStringBuilder)
	(Int32 length)
	(Pointer buffer)
	(Int32 capacity)
)

(function NewTokenBuilder -> (IStringBuilder sb):
	(Sys.Strings.Native.NewStringBuilder 64 -> sb)
)

(function NewPathBuilder -> (IStringBuilder sb):
	(Sys.Strings.Native.NewStringBuilder 260 -> sb)
)

(function NewParagraphBuilder -> (IStringBuilder sb):
	(Sys.Strings.Native.NewStringBuilder 1024 -> sb)
)

(function NewStringBuilder (Int32 capacity) -> (IStringBuilder sb):
	(Sys.Strings.Native.NewStringBuilder capacity -> sb)
)

(alias NewTokenBuilder     Sys.Type.NewTokenBuilder)
(alias NewPathBuilder      Sys.Type.NewPathBuilder)
(alias NewParagraphBuilder Sys.Type.NewParagraphBuilder)
(alias NewStringBuilder    Sys.Type.NewStringBuilder)

(method FastStringBuilder.Destruct -> :
	(Sys.Strings.Native.DestructStringBuilder this)
)

(method FastStringBuilder.AppendIString  (IString s) -> :
	(Sys.Strings.Native.FastStringBuilderAppendIString this s.Buffer s.Length)
)

(method FastStringBuilder.AppendIStringBuilder  (IStringBuilder s) -> :
	(Sys.Strings.Native.FastStringBuilderAppendIString this s.Buffer s.Length)
)

(method FastStringBuilder.AppendChar  (Int32 asciiValue) -> :
	(Sys.Strings.Native.FastStringBuilderAppendChar this asciiValue)
)

(method FastStringBuilder.AppendInt32  (Int32 x) -> :
	(Sys.Strings.Native.FastStringBuilderAppendInt32 this x)
)

(method FastStringBuilder.AppendInt64  (Int64 x) -> :
	(Sys.Strings.Native.FastStringBuilderAppendInt64 this x)
)

(method FastStringBuilder.AppendFloat32  (Float32 x) -> :
	(Sys.Strings.Native.FastStringBuilderAppendFloat32 this x)
)

(method FastStringBuilder.AppendFloat64  (Float64 x) -> :
	(Sys.Strings.Native.FastStringBuilderAppendFloat64 this x)
)

(method FastStringBuilder.AppendBool  (Bool x) -> :
	(Sys.Strings.Native.FastStringBuilderAppendBool this x)
)

(method FastStringBuilder.AppendPointer (Pointer x) -> :
	(Sys.Strings.Native.FastStringBuilderAppendPointer this x)
)

(method FastStringBuilder.AppendAsDecimal (Sys.Type.Formatters.AsDecimal x) -> :
	(Sys.Strings.Native.FastStringBuilderAppendAsDecimal this)
)

(method FastStringBuilder.AppendAsHex (Sys.Type.Formatters.AsHex x) -> :
	(Sys.Strings.Native.FastStringBuilderAppendAsHex this)
)

(method FastStringBuilder.AppendAsSpecE (Sys.Type.Formatters.AsSpecE null) -> :
	(Sys.Strings.Native.FastStringBuilderAppendAsSpec this 1)
)

(method FastStringBuilder.AppendAsSpecF (Sys.Type.Formatters.AsSpecF null) -> :
	(Sys.Strings.Native.FastStringBuilderAppendAsSpec this 2)
)

(method FastStringBuilder.AppendAsSpecG (Sys.Type.Formatters.AsSpecG null) -> :
	(Sys.Strings.Native.FastStringBuilderAppendAsSpec this 3)
)

(method FastStringBuilder.AppendAsNewLine (Sys.Type.Formatters.AsNewLine null) -> :
	(this.AppendIString "&r&n")
)

(method FastStringBuilder.SetLength (Int32 length) -> :
	(Sys.Strings.Native.FastStringBuilderSetLength this length)
)

(method FastStringBuilder.SetFormat (Sys.Type.Int32 precision) (Sys.Type.Int32 width) (Sys.Type.Bool isZeroPrefixed) (Sys.Type.Bool isRightAligned) -> :
	(Sys.Strings.Native.FastStringBuilderSetFormat this precision width isZeroPrefixed isRightAligned)
)

(method FastStringBuilder.Clear -> :
	(Sys.Strings.Native.FastStringBuilderClear this)	
)

(method FastStringBuilder.Buffer -> (Sys.Type.Pointer buffer):
	(buffer = this.buffer)
)

(method FastStringBuilder.Length -> (Sys.Type.Int32 length):
	(length = this.length)
)

(method FastStringBuilder.AppendSubstring (IString s) (Int32 startPos) (Int32 numberOfChars) -> :
	(Sys.Strings.Native.FastStringBuilderAppendSubstring this s.Buffer s.Length startPos numberOfChars)
)

(method FastStringBuilder.Replace (Int32 startPosition) (IString from)(IString to)-> :
	(Sys.Strings.Native.FastStringBuilderReplace this startPosition from to)
)

(method FastStringBuilder.ToUpper (Int32 start) (Int32 end) -> :
	(Sys.Strings.Native.FastStringBuilderSetCase this start end true)
)

(method FastStringBuilder.ToLower (Int32 start) (Int32 end) -> :
	(Sys.Strings.Native.FastStringBuilderSetCase this start end false)
)

(method FastStringBuilder.StripLeft (Int32 left) -> :
	(Sys.Strings.Native.FastStringBuilderStripLeft this left)
)

(method FastStringBuilder.ThrowIfAppendWouldTruncate -> :
	(Sys.Strings.Native.FastStringBuilderThrowIfAppendWouldTruncate this)
)

(function StringEndsWith (IString bigString) (IString suffix) -> (Bool isSo):
	(Sys.Strings.Native.StringEndsWith bigString suffix -> isSo)
)

(function StringStartsWith (IString bigString) (IString prefix) -> (Bool isSo):
	(Sys.Strings.Native.StringStartsWith bigString prefix -> isSo)
)

(function StringCompare (IString s) (IString t) -> (Int32 diff):
	(Sys.Strings.Native.StringCompare s.Buffer t.Buffer -> diff)
)

(function StringCompareI (IString s) (IString t) -> (Int32 diff):
	(Sys.Strings.Native.StringCompareI s.Buffer t.Buffer -> diff)
)

(function ExactMatch (IString s) (IString t) -> (Bool match):
	(Int32 diff)
	(Sys.Strings.Native.StringCompare s.Buffer t.Buffer -> diff)
	(if (diff == 0) (match = true) else (match = false))
)

(function FindLeft (IString container) (Int32 startPos) (IString substring) -> (Int32 position):
	(Sys.Strings.Native.StringFindLeft container.Buffer container.Length startPos substring.Buffer substring.Length false -> position)
)

(function FindRight (IString container) (Int32 leftPos) (Int32 rightPos) (IString substring) -> (Int32 position):
	(Sys.Strings.Native.StringFindRight container.Buffer container.Length leftPos rightPos substring.Buffer substring.Length false -> position)
)

(function FindLeftI (IString container) (Int32 startPos) (IString substring) -> (Int32 position):
	(Sys.Strings.Native.StringFindLeft container.Buffer container.Length startPos substring.Buffer substring.Length true -> position)
)

(function FindRightI (IString container) (Int32 leftPos) (Int32 rightPos) (IString substring) -> (Int32 position):
	(Sys.Strings.Native.StringFindRight container.Buffer container.Length leftPos rightPos substring.Buffer substring.Length true -> position)
)

(function MakeSysSlashes (IStringBuilder sb) -> : (Sys.Strings.Native.MakeSysSlashes sb))
(alias MakeSysSlashes Sys.Type.Strings.MakeSysSlashes)

(archetype Sys.Type.Strings.ForEachSubstring (IStringBuilder sb) (Int32 index) ->)

(function StringSplit (IStringBuilder sb) (IString container) (IString sep) (Sys.Type.Strings.ForEachSubstring f) -> :
	(if ((sep.Length == 0) or (container.Length == 0)) (return))
	(Int32 len = container.Length)
	(Int32 i = 0)
	(Int32 last = 0)
	(Int32 index = 0)
	(while (i < len)
		(i = (Strings.FindLeftNoCase container i sep))
		(if (i < 0) (sb.AppendSubstring container last container.Length) (f sb index) (break))
		(sb.AppendSubstring container last (i - last))
		(last = (i + sep.Length))
		(i = (i + sep.Length))
		(f sb index)
		(index += 1)
	)
)

(interface Sys.Type.IStringBuilder
	(extends Sys.Type.IString)
	(attribute builder Append) // if we have a variable s of type IStringBuilder then (s X) calls (s.AppendTypeX X), e.g (s 1) calls (s.AppendInt32 1)
	(AppendChar (Int32 asciiValue) -> )
	(AppendInt32 (Int32 x) -> )
	(AppendInt64 (Int64 x) -> )
	(AppendFloat32 (Float32 x) -> )
	(AppendFloat64 (Float64 x) -> )
	(AppendBool (Bool x) -> )
	(AppendPointer (Pointer x) -> )
	(AppendIString (IString s) -> )
	(AppendIStringBuilder (IStringBuilder other) -> )
	(AppendAsHex (AsHex null) ->)
	(AppendAsDecimal (AsDecimal null) ->)
	(AppendAsSpecE (AsSpecE null) ->)
	(AppendAsSpecF (AsSpecF null) ->)
	(AppendAsSpecG (AsSpecG null) ->)
	(AppendAsNewLine (AsNewLine null) ->)
	(AppendSubstring (IString s) (Int32 startPos) (Int32 numberOfChars) ->)
	(Replace (Int32 startPosition) (IString from)(IString to) ->)
	(SetFormat (Sys.Type.Int32 precision) (Sys.Type.Int32 width) (Sys.Type.Bool isZeroPrefixed) (Sys.Type.Bool isRightAligned) ->)
	(SetLength (Int32 length) ->)
	(StripLeft (Int32 leftPos) ->)
	(ToUpper (Int32 start) (Int32 end) ->)
	(ToLower (Int32 start) (Int32 end) ->)
	(Clear ->)
	(ThrowIfAppendWouldTruncate ->)
)

(namespace Sys.Type.Strings)
	(alias StringCompare Sys.Type.Strings.Compare)
	(alias StringCompareI Sys.Type.Strings.CompareInsensitive)
	(alias ExactMatch Sys.Type.Strings.IsExactMatch)
	(alias FindLeft Sys.Type.Strings.FindLeftWithCase)
	(alias FindRight Sys.Type.Strings.FindRightWithCase)
	(alias FindLeftI Sys.Type.Strings.FindLeftNoCase)
	(alias FindRightI Sys.Type.Strings.FindRightNoCase)
	(alias StringSplit Sys.Type.Strings.Split)
	(alias StringStartsWith Sys.Type.Strings.StartsWith)
	(alias StringEndsWith Sys.Type.Strings.EndsWith)

(namespace Sys.Type.Formatters)
	(alias FormatDecimal Sys.Type.Formatters.Decimal)
	(alias FormatHex Sys.Type.Formatters.Hex)
	(alias FormatUnsigned Sys.Type.Formatters.Unsigned)
	(alias FormatSpecE Sys.Type.Formatters.SpecE)
	(alias FormatSpecF Sys.Type.Formatters.SpecF)
	(alias FormatSpecG Sys.Type.Formatters.SpecG)
	(alias FormatNewLine Sys.Type.Formatters.NewLine)
	(alias FormatFormat Sys.Type.Formatters.Format)

(factory Sys.Type.Memo IString (IString s):
	(construct MemoImpl s)
)

(function Preserve (IString variant)->(IString invariant):
	(IString memo (Sys.Type.Memo variant))
	(invariant = memo)
)

(alias Preserve Sys.Type.Strings.Preserve)

(function NullString -> (IString nullString):
)

(alias NullString Sys.Type.NullString)

// Example (#build s 1 2 3 4) maps to (s 1)(s 2)(s 3)(s 4), used with such as stringbuilders and serializers
(macro Sys.Type.build in out
	(ValidateSubscriptRange in.ChildCount 3 1000000 "Usage: (##build <variable-name> <arg1> ... <argN>)")
	(ConvertArgsToExpressionPairs in (in 1) out 2)
)

(using Sys)
(using Sys.Reflection)

(function ExpandStringMacro (IExpression in)(IExpressionBuilder out)-> :
	(ValidateSubscriptRange in.ChildCount 3 1000000 "Usage: (##string <variable-name> <capacity> <arg1> ... <argN>)")
	(out.AddAtomic "IStringBuilder")
	(out.Copy (in 1))
	(out.AddAtomic "=")
	(IExpressionBuilder factoryCall = out.AddCompound)
	(factoryCall.AddAtomic "NewStringBuilder")
	(factoryCall.Copy (in 2))
	
	(if (in.ChildCount > 3)
		(IExpressionBuilder parent = out.TransformParent)
		(Int32 outIndex)
		(parent.IndexOf in -> outIndex)
		
		(IExpressionBuilder builderSequence = (parent.InsertCompoundAfter outIndex))	
		(ConvertArgsToExpressionPairs in (in 1) builderSequence 3)
	)
)

(alias ExpandStringMacro Sys.Type.ExpandStringMacro)

// Example (#string sb 64 "Position:" 37 "," 124) maps to (IStringBuilder sb = (NewStringBuilder 64)) ((sb.AppendIString "Position:")(sb.AppendInt32 37) (sb.AppendIString ",") (sb.AppendInt32 124))
(macro Sys.Type.string in out
	(ExpandStringMacro in out)
)

(function ExpandStringMacroEx (IString factoryName) (IExpression in)(IExpressionBuilder out)(Bool doNotTruncate)-> :
	(out.AddAtomic "IStringBuilder")
	(IExpression variableName = (in 1))
	(out.AddAtomic variableName.Text)
	(out.AddAtomic "=")
	(IExpressionBuilder factoryCall = out.AddCompound)
	(factoryCall.AddAtomic factoryName)
	
	(if (in.ChildCount > 2)
		(IExpressionBuilder parent = out.TransformParent)
		(Int32 outIndex)
		(parent.IndexOf in -> outIndex)
		
		(IExpressionBuilder builderSequence = (parent.InsertCompoundAfter outIndex))

		(if doNotTruncate
			(IStringBuilder sb = (NewPathBuilder))
			(sb variableName.Text)
			(sb	".ThrowIfAppendWouldTruncate")
			(builderSequence.AddAtomic sb)		
			(outIndex += 1)
			(builderSequence = (parent.InsertCompoundAfter outIndex))
		)
		
		(ConvertArgsToExpressionPairs in variableName builderSequence 2)
	)
)

// Usage (#token <variable-name> args...) which expands to (IStringBuilder <variable-name> = (NewTokenBuilder)) ((<variable-name> args1) ... (<variable-name> argsN)).
// Tokens have 64 character capacity (including the terminating nul char)
(macro Sys.Type.token in out
	(ValidateSubscriptRange in.ChildCount 2 1000000 "Usage: (##token <variable-name> args...)")
	(ExpandStringMacroEx "NewTokenBuilder" in out true)
)

// Usage (#path <variable-name> args...) which expands to (IStringBuilder <variable-name> = (NewPathBuilder)) ((<variable-name> args1) ... (<variable-name> argsN)).
// Paths have 260 character capacity (including the terminating nul char)
(macro Sys.Type.path in out
	(ValidateSubscriptRange in.ChildCount 2 1000000 "Usage: (##path <variable-name> args...)")
	(ExpandStringMacroEx "NewPathBuilder" in out true)
)

// Usage (#paragraph <variable-name> args...) which expands to (IStringBuilder <variable-name> = (NewParagraphBuilder)) ((<variable-name> args1) ... (<variable-name> argsN)).
// Paragraphs have 1024 character capacity (including the terminating nul char)
(macro Sys.Type.paragraph in out
	(ValidateSubscriptRange in.ChildCount 2 1000000 "Usage: (##paragraph <variable-name> args...)")
	(ExpandStringMacroEx "NewParagraphBuilder" in out false)
)

