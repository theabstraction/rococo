(' file.type dystopia.level)

(' #include 
	"!scripts/dystopia.sxh.sxy" 
	"!scripts/dystopia.defs.sxy"
)

(namespace EntryPoint)
	(alias Main EntryPoint.Main)
	
(using Sys.Maths)
(using Dystopia.Entities)
(using Dystopia.Callbacks)

(function OnHavingMetProfessor (Int64 playerId) (Int64 professorId)-> :
	(Dystopia.Gui.IGui gui (Dystopia.Gui.GetGui))	
	(Vec2i span = 800 600)
	(gui.ShowDialogBox span 100 30 "A most unexpected meeting" "Professor Black has instructed me on my next target. Find MMM and get the package." ">Continue...=0" )	
)

(function OnHavingMetMMM (Int64 playerId) (Int64 mmmId)-> :
	(Dystopia.Gui.IGui gui (Dystopia.Gui.GetGui))	
	(Vec2i span = 800 600)
	(gui.ShowDialogBox span 100 30 "Met MMM" "You must take this to a higher level." ">Continue...=OnMMMComplete" )	
)

(function OnMMMComplete -> :
	(Dystopia.Levels.ILevel level (Dystopia.Levels.GetLevel))
    (level.SetLevel "!levels/level2.sxy")
)

(function Main (Int32 arg) -> (Int32 exitCode): 
	(Dystopia.Meshes.IMeshes meshes (Dystopia.Meshes.GetMeshes))
	(meshes.Load "!mesh/cube.sxy" 1)
	(meshes.Load "!mesh/road.sxy" 2)
	(meshes.Load "!mesh/human.sxy" 3)
	(meshes.Load "!mesh/rifle.sxy" 4)
	
	(Dystopia.Levels.ILevel level (Dystopia.Levels.GetLevel))
	(level.Clear)
	
	(Int64 playerId)
	(Int64 professorId)
	(Int64 mmmId)

	(
		(Vec3 position = 0 0 0)
		(playerId = (level.AddAlly position 3))
		(level.SetPlayerId playerId)
	)

	(
		(Vec3 position = 5 5 0)
		(professorId = (level.AddAlly position 3))
	)

	(
		(Vec3 position = 10 5 0)
		(mmmId = (level.AddAlly position 3))
	)
	
	(level.AddStreetName "Newport Road")
	(level.AddStreetName "Lichfield Road")
	(level.AddStreetName "Wolverhampton Road")
	(level.AddStreetName "Stone Road")
	(level.AddStreetName "Eccleshall Road")
	(level.AddStreetName "Weston Road")
	(level.AddStreetName "Weeping Cross")
	(level.AddStreetName "Cannock Road")
	(level.GenerateCity "Stafford" 250)
//	(level.PopulateCity 2.5)
	
	(Dystopia.Journal.IJournal journal (Dystopia.Journal.GetJournal))
	
	(journal.AddHistory "Diary entry: 4th July"
		"Being bankrupt didn't get me the sack. Keeping it secret however... I swear I'll get that bastard Jack.")

	(journal.AddHistory "Diary entry: 7th July"
		"Stock market crash and conspiracy theorists come out of the woodwork. Just another day in the economy. Good news, Jack's suppliers went bump. Let's hope his business follows suit, and soon.")

	(journal.AddHistory "Diary entry: 8th July"
		"Much silly hysteria all over the papers. People panic buying. I'll get some stuff tomorrow, can't be arsed to walk into town today.")

	(journal.AddHistory "Diary entry: 9h July"
		"Spent six hours in that bloody queue. Got to the front and the shop was almost bare. Gov should be sending lorries around soon I guess.")

	(journal.AddHistory "Diary entry: 13th July"
		"Fridge is empty, stomach is rumbling, shops empty. Beginning to think Gov will not hit Stafford until next week.")

	(journal.AddHistory "Diary entry: 20th July"
		"If I don't get food soon, I will go out and steal some. Beginning to think Gov will never hit stafford.")

	(journal.AddHistory "Diary entry: 22nd July"
		"This may be my last diary entry. Just saw a vid on youtube, gov lorries never left depot. People fighting in street now. Looters burning vehicles two miles away. A mob of them. Police sent home - no money. This is the end. Goodbye world.")

	(journal.AddHistory "Diary entry: 23rd July"
		"Plan:&n&tKill looters.&n&tGet food.&n&tGet money&n&tGet down to that depot and kick jobsworths' arses.")

	(Int64 professorGoal = (journal.AddGoalMeet "Meet the Professor" "The professor has the package. Collect it before he is assasinated." playerId professorId 2.0 OnHavingMetProfessor))
	(Int64 mmmGoal = (journal.AddGoalMeet "Meet the Marsh Mellow Maker" "MMM has the package. Collect it before he is assasinated." playerId mmmId 2.0 OnHavingMetMMM))
	(journal.CompleteFirst mmmGoal professorGoal)
			
	(exitCode = 0)
)
